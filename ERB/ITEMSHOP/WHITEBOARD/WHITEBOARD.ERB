@WHITEBOARD_SCENE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LOOPSWITCH
#DIM DYNAMIC CURSORV

#DIM DYNAMIC WBLISTA,WHITEBOARD_MAX
#DIM DYNAMIC WBLISTA_LEN
#DIM DYNAMIC TOERASE

#DIMS DYNAMIC CLEARS = "ＣＬＥＡＲ！！"

;흰 배경 출력
LOOPSWITCH = 1

WBLISTA_LEN = 0
FOR LCOUNT,1,WHITEBOARD_MAX+1
    RESULT = 0
    TRYCALLFORM WHITEBOARD_DATA_{LCOUNT},WHITEBOARD_ENABLE
    SIF !RESULT
        CONTINUE
    WBLISTA:WBLISTA_LEN = LCOUNT
    SIF LCOUNT == CURRENT_WHITEBOARD
        CURSORV = WBLISTA_LEN
    WBLISTA_LEN ++
NEXT
DO
    ;클리어 여부 검사
    TRYCALLFORM WHITEBOARD_DATA_{WBLISTA:CURSORV},WHITEBOARD_ENABLE
    RESULT:1 = RESULT
    TRYCALLFORM WHITEBOARD_DATA_{WBLISTA:CURSORV},WHITEBOARD_CLEAR    
    IF RESULT && RESULT:1
        REPEAT 7
            FOR LCOUNT,0,COUNT+1
                PRINTS CHARATU(CLEARS,LCOUNT)
            NEXT
            PRINTL
            PRINT_SPACE 100 + (COUNT*100)            
            FOR LCOUNT,COUNT+1,7+1
                PRINTS CHARATU(CLEARS,LCOUNT)
            NEXT
            PRINTL
            TWAIT 25,1
            IF COUNT < 6
                CLEARLINE 2
                REUSELASTLINE
            ENDIF
        REND
        TRYCALLFORM WHITEBOARD_DATA_{WBLISTA:CURSORV},WHITEBOARD_REWARD
        RESTART
    ENDIF

    TOERASE = 23
    ;하얀 사각형 이미지 생성 후 출력
    CBGCLEAR
    GCREATE 0,620,400
    GSETBRUSH 0, 0xFFFFFFFF
    GFILLRECTANGLE 0, 0, 0, 620, GETCONFIG("一行の高さ")*20
    CBGSETG 0, 0, GETCONFIG("一行の高さ")*3, 1

    ;화이트보드 내부 문자열 출력
    TRYCCALLFORM WHITEBOARD_DATA_{WBLISTA:CURSORV},WHITEBOARD_PRINT
    CATCH
        FOR LCOUNT,0,20
            PRINTL
        NEXT
    ENDCATCH

    ;활성화/클리어 여부 출력
    IF WBLISTA:CURSORV == CURRENT_WHITEBOARD
        TRYCALLFORM WHITEBOARD_DATA_{WBLISTA:CURSORV},WHITEBOARD_CLEAR
        IF !RESULT
            SETCOLORBYNAME Orange
            PRINTL 활성화 중
            RESETCOLOR
        ELSE
            SETCOLORBYNAME Green
            PRINTL 클리어!
            RESETCOLOR
        ENDIF
    ELSE
        PRINTL
    ENDIF
    PRINTBUTTON "[a] 이전 메모","a"
    PRINTBUTTON "[d] 다음 메모","d"
    SIF WBLISTA:CURSORV == CURRENT_WHITEBOARD
        SETCOLORBYNAME DimGray
    PRINTBUTTON "[u] 활성화", "u"
    RESETCOLOR
    PRINTBUTTON "[x] 돌아간다","x"
    PRINTL
    ONEINPUTS
    SELECTCASE RESULTS
        CASE "a"
            SIF CURSORV > 0
                CURSORV --
        CASE "d"
            SIF CURSORV < WBLISTA_LEN-1
                CURSORV ++
        CASE "u"
            CURRENT_WHITEBOARD = WBLISTA:CURSORV
        CASE "x"
            CBGCLEAR
            LOOPSWITCH = 0
        CASE ""
            TOERASE -= 1
    ENDSELECT
    CLEARLINE TOERASE
    REUSELASTLINE
LOOP LOOPSWITCH

@WHITEBOARD_COUNTING_UP(KEYV=0,KEYS="")
#DIM DYNAMIC KEYV
#DIMS DYNAMIC KEYS
TRYCALLFORM WHITEBOARD_DATA_{CURRENT_WHITEBOARD},WHITEBOARD_INCREASE,KEYV,KEYS