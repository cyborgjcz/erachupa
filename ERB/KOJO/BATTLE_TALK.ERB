@BATTLE_TALK(TYPE,CALLER,RESPONDING=0,KNUM=0)
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC TYPE;구상 종류
#DIM DYNAMIC CALLER;말한애 (주의:1또는2) 비트XOR연산(^3)으로 뒤집는다
#DIM DYNAMIC RESPONDING;응답중인가?
#DIM DYNAMIC KNUM;종류 중 구상번호

#DIM DYNAMIC RESPONSE;응답할애 (주의:1또는2) 현재 응답중인경우 무시
#DIMS DYNAMIC RESSTR;응답하는 중임을 표기하는 문자열,기본 공백
#DIM DYNAMIC C_ID;말한애 CSV 번호
#DIM DYNAMIC R_ID;답하는애 CSV 번호

#DIM DYNAMIC TRANSNUMV;변신하고 있는 대상 유형
#DIMS DYNAMIC TRANSSTR;그것을 포함하는 문자열

RESSTR =
TRANSSTR =
;변신중인 경우 TRANSSTR을 FORM식으로 작성한다
IF CFLAG:(FLAG:CALLER):변신
    TRANSNUMV = CFLAG:(FLAG:CALLER):변신가능
    TRANSSTR '= @"_TR{TRANSNUMV}"
ENDIF
C_ID = GET_ID(FLAG:CALLER)
IF !RESPONDING
    RESPONSE = CALLER^3
    R_ID = GET_ID(FLAG:RESPONSE)
    TFLAG:전투회화응답자 = RESPONSE
    ;전용대사판정. 전용대사는 응답을 상정하지 않음
    TRYCCALLFORM KOJO_BATTLE_%TYPE%{KNUM}_K{C_ID}_SP_K{R_ID}%TRANSSTR%
        TFLAG:전용회화플래그 = 1
        RETURN 1
    CATCH
        TRYCCALLFORM KOJO_BATTLE_%TYPE%_K{C_ID}_SP_K{R_ID}%TRANSSTR%
            TFLAG:전용회화플래그 = 1
            RETURN 1
        CATCH
            ;전용대사없음
        ENDCATCH
    ENDCATCH
ELSEIF RESPONDING
    ;응답하는 경우, 전용회화 플래그가 서있지 않다면 RESSTR을 작성한 뒤 메인콜로 이행
    IF TFLAG:전용회화플래그
        TFLAG:전용회화플래그 = 0
        FOR LCOUNT,44,51
            TFLAG:LCOUNT = 0
        NEXT
        RETURN 1
    ENDIF
    RESSTR = RES_
ENDIF

;메인콜
TRYCCALLFORM KOJO_BATTLE_%TYPE%{KNUM}_%RESSTR%K{C_ID}%TRANSSTR%
    IF RESPONDING
        TFLAG:전용회화플래그 = 0
        FOR LCOUNT,44,51
            TFLAG:LCOUNT = 0
        NEXT
    ENDIF
    RETURN 1
CATCH
    TRYCCALLFORM KOJO_BATTLE_%TYPE%_%RESSTR%K{C_ID}%TRANSSTR%
        IF RESPONDING
            TFLAG:전용회화플래그 = 0
            FOR LCOUNT,44,51
                TFLAG:LCOUNT = 0
            NEXT
        ENDIF
        RETURN 1
    CATCH
        DEBUGPRINTFORML KOJO_BATTLE_%TYPE%_%RESSTR%K{C_ID}%TRANSSTR% 출력 실패
        RETURN 0
    ENDCATCH
ENDCATCH