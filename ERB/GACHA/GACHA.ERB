@GACHA_SCENE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC GACHALISTA,GACHA_NUM;참조하는 생성된 조건에 일치하는 가챠 리스트
#DIM DYNAMIC GACHALISTA_LEN;리스트의 길이
#DIM DYNAMIC CURSORV;커서
#DIM DYNAMIC PAGEV;넘긴 페이지 번호
#DIM DYNAMIC PAGEMAX;최대 페이지 수
#DIM DYNAMIC PAGELEN = 6;한 페이지의 길이
#DIM DYNAMIC LOOPSWITCH
#DIM DYNAMIC TOERASE
#DIM DYNAMIC GACHA_OPEN
#DIMS DYNAMIC GACHA_NAME
#DIMS DYNAMIC GACHA_HELP
#DIM DYNAMIC GACHA_PRICE
#DIM DYNAMIC GACHA_LEN,3;각 가챠 레어도마다 등장하는 아이템의 종류 수
#DIM DYNAMIC GACHA_ITEMA,3,100;각 가챠 레어도마다 등장하는 아이템 종류 보존
#DIM DYNAMIC GACHA_RAN

LOOPSWITCH = 1
DO
    CBGCLEAR
    GCREATEFROMFILE 0, @"white_sora.png"
    CBGSETG 0, 620, -20, 1
    GACHA_RAN = 0
    TOERASE = 20 ;HERE
    GACHALISTA_LEN = 0
    VARSET GACHALISTA
    FOR LCOUNT,1,GACHA_NUM+1
        VARSET GACHA_OPEN
        TRYCALLFORM GACHA_DATA_{LCOUNT},GACHA_OPEN,GACHA_NAME,GACHA_HELP,GACHA_LEN,GACHA_ITEMA,GACHA_PRICE
        SIF !GACHA_OPEN
            CONTINUE
        GACHALISTA:GACHALISTA_LEN = LCOUNT
        GACHALISTA_LEN ++
    NEXT
    PAGEMAX = (GACHALISTA_LEN - 1) / PAGELEN
    FOR LCOUNT,0,PAGELEN
        IF !GACHALISTA:LCOUNT && LCOUNT < PAGELEN
            PRINTL
            PRINTL
            PRINTL
            CONTINUE
        ENDIF
        IF CURSORV == (PAGEV * PAGELEN)+LCOUNT
            SETCOLOR 0xfee12b
            PRINTL ♪
            RESETCOLOR
        ELSE
            PRINTL
        ENDIF
        TRYCALLFORM GACHA_DATA_{GACHALISTA:LCOUNT},GACHA_OPEN,GACHA_NAME,GACHA_HELP,GACHA_LEN,GACHA_ITEMA,GACHA_PRICE
        PRINTFORML %GACHA_NAME% ({GACHA_PRICE}머니)
        PRINTSL GACHA_HELP
    NEXT
    SIF PAGEV > 0
        PRINTBUTTON "[a] ← 이전 페이지" ,"a"
    PRINT |
    SIF PAGEV < PAGEMAX && PAGEV != PAGEMAX
        PRINTBUTTON "[d] → 다음 페이지","d"
    PRINTL
    PRINTBUTTON "[w] ↑ ","w"
    PRINTBUTTON "[s] ↓ ","s"
    PRINTBUTTON "[u] 결정","u"
    PRINTBUTTON "[x] 돌아간다","x"
    ONEINPUTS
    SELECTCASE RESULTS
        CASE "u"
            ;HERE 여기 대가 지불
            TRYCALLFORM GACHA_DATA_{GACHALISTA:CURSORV},GACHA_OPEN,GACHA_NAME,GACHA_HELP,GACHA_LEN,GACHA_ITEMA,GACHA_PRICE
            IF MONEY < GACHA_PRICE
                PRINTL 머니가 부족합니다.
                WAITANYKEY
                TOERASE ++
            ELSE
                MONEY -= GACHA_PRICE
                CBGCLEAR
                CALL RUN_GACHA(GACHALISTA:CURSORV)
                GACHA_RAN = 1
            ENDIF
        CASE "x"
            LOOPSWITCH = 0
        CASE "a"
            IF PAGEV > 0
                PAGEV -= 1
                CURSORV -= PAGELEN
                SIF CURSORV < 0
                    CURSORV = 0
            ENDIF
        CASE "d"
            IF PAGEV < PAGEMAX
                PAGEV += 1
                CURSORV += PAGELEN
                SIF CURSORV > (GACHALISTA_LEN - 1)
                    CURSORV = (GACHALISTA_LEN - 1)
            ENDIF
        CASE "w"
            IF CURSORV > 0
                SIF CURSORV % PAGELEN == 0
                    PAGEV -= 1
                CURSORV -= 1
            ENDIF
        CASE "s"
            IF CURSORV < GACHALISTA_LEN-1
                SIF CURSORV % PAGELEN == (PAGELEN-1)
                    PAGEV += 1
                CURSORV += 1
            ENDIF
    ENDSELECT
    IF LOOPSWITCH == 1 && !GACHA_RAN
        CLEARLINE TOERASE
        SIF RESULTS != ""
            CLEARLINE 1
        REUSELASTLINE
    ENDIF
LOOP LOOPSWITCH
CBGCLEAR
;선택 및 지불이 완료된 후 실제 가챠 회전 판정.
@RUN_GACHA(KEYV)
#DIM DYNAMIC KEYV;가챠 종류를 결정
#DIM DYNAMIC REWARDNUM = 10
#DIM DYNAMIC REWARDA,10
#DIM DYNAMIC FINALREWARDA,10
#DIM DYNAMIC GACHA_OPEN
#DIMS DYNAMIC GACHA_NAME
#DIMS DYNAMIC GACHA_HELP
#DIM DYNAMIC GACHA_PRICE
#DIM DYNAMIC GACHA_LEN,3;각 가챠 레어도마다 등장하는 아이템의 종류 수
#DIM DYNAMIC GACHA_ITEMA,3,100;각 가챠 레어도마다 등장하는 아이템 종류 보존
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LUCKYPOINT;좋은결과 연출에 참조하는 가산점.
#DIM DYNAMIC LASTREWARD;출력을 위해 보존하는 보상 아이템 값
#DIM DYNAMIC MULTIREWARD;복수의 아이템을 획득하는 양을 보존

TRYCCALLFORM GACHA_DATA_{KEYV},GACHA_OPEN,GACHA_NAME,GACHA_HELP,GACHA_LEN,GACHA_ITEMA,GACHA_PRICE
CATCH
    PRINTL 가챠 데이터를 불러오는데 실패했습니다.
    RETURN 1
ENDCATCH

;우선 10번의 기회에 획득하는 레어도부터 판정
FOR LCOUNT,0,REWARDNUM
    SELECTCASE RAND:100
        CASE 0 TO 9
            REWARDA:LCOUNT = 0
            LUCKYPOINT += 50
        CASE 10 TO 44
            REWARDA:LCOUNT = 1
            LUCKYPOINT += 3
        CASE 45 TO 99
            REWARDA:LCOUNT = 2
            LUCKYPOINT += 1
    ENDSELECT
NEXT
FOR LCOUNT,0,REWARDNUM
    ;여기서 실제로 아이템 획득
    FINALREWARDA:LCOUNT = GACHA_ITEMA:(REWARDA:LCOUNT):(RAND:(GACHA_LEN:(REWARDA:LCOUNT)))
NEXT
;레어 획득 시 연출이 달라지는 묘사
SELECTCASE LUCKYPOINT
    CASE 50 TO 500
        ;SSR 포함
        CALL BUTTERFLY_TEXTANIME(0)
    CASE 25 TO 49
        ;SR 다량
        CALL BUTTERFLY_TEXTANIME(1)
    CASE 10 TO 24
        ;참가상......
        CALL BUTTERFLY_TEXTANIME(2)
ENDSELECT

ARRAYSORT FINALREWARDA
LASTREWARD = FINALREWARDA:0
FOR LCOUNT,0,REWARDNUM
    IF LASTREWARD != FINALREWARDA:LCOUNT
        ITEM:LASTREWARD += MULTIREWARD
        PRINTFORML %조사처리(ITEMNAME:LASTREWARD,"을")% {MULTIREWARD} 개 얻었습니다.
        TWAIT 25,1
        MULTIREWARD = 0
    ENDIF
    LASTREWARD = FINALREWARDA:LCOUNT
    MULTIREWARD += 1
NEXT
ITEM:LASTREWARD += MULTIREWARD
PRINTFORML %조사처리(ITEMNAME:LASTREWARD,"을")% {MULTIREWARD} 개 얻었습니다.
WAITANYKEY

@BUTTERFLY_TEXTANIME(KEYV)
#DIM DYNAMIC KEYV
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC MAXV = 21
#DIM DYNAMIC YELLOWCOLORVA,20 = 0xFFFFFB,0xFFFFE0,0xFFFFCC,0xFFFFAD,0xFFFFCC,0xFFFFE0,0xFFFFFB,0xFFFFE0,0xFFFFCC,0xFFFFAD,0xFFFFCC,0xFFFFE0,0xFFFFFB,0xFFFFE0,0xFFFFCC,0xFFFFAD,0xFFFFCC,0xFFFFE0,0xFFFFFB,0xFFFFFF

SIF KEYV == 2
    MAXV = 11


PRINTL
FOR LCOUNT,0,MAXV
    SIF LCOUNT % 2 == 1
        PRINTL
    FOR LCOUNT2,0,LCOUNT
        IF (LCOUNT % 2 == 0) && (LCOUNT2 % 2 == 0)
            PRINT 　
        ELSEIF (LCOUNT2 % 2 == 0)
            PRINT ．
        ENDIF            
    NEXT
    IF LCOUNT % 4 == 0
        PRINT Ƹ Ӂ Ʒ
    ELSE
        PRINT ƸӁƷ
    ENDIF
    IF LCOUNT % 2 == 0
        PRINTL
        FOR LCOUNT2,0,LCOUNT
            SIF LCOUNT2 % 2 == 0
                PRINT ．
        NEXT
    ENDIF
    TWAIT 125,1
    CLEARLINE 2
    REUSELASTLINE
NEXT
FOR LCOUNT,0,MAXV
    FOR LCOUNT2,0,MAXV
        IF KEYV == 0 && LCOUNT2 % 2 == 0
            SETCOLORBYNAME Yellow
            IF LCOUNT2 <= LCOUNT
                PRINT ＠
            ELSE 
                PRINT ＊
            ENDIF
            RESETCOLOR
        ELSEIF LCOUNT2 % 2 == 0
            PRINT 　
        ENDIF
    NEXT
    IF LCOUNT >= 39
        SETCOLOR YELLOWCOLORVA:0
    ELSEIF LCOUNT >= 20
        SETCOLOR YELLOWCOLORVA:(LCOUNT - 20)
    ELSE
        SETCOLOR YELLOWCOLORVA:LCOUNT
    ENDIF   
    PRINT Ƹ Ӂ Ʒ
    RESETCOLOR
    PRINTL
    FOR LCOUNT2,0,MAXV
        IF KEYV == 0 && LCOUNT2 % 2 == 0
            SETCOLORBYNAME LightGreen
            PRINT ∇
            RESETCOLOR
        ELSEIF LCOUNT2 % 2 == 0
            PRINT ．
        ENDIF
    NEXT
    IF LCOUNT < (MAXV-1)
        TWAIT 50,1
        CLEARLINE 2
        REUSELASTLINE
    ENDIF
NEXT
PRINTL