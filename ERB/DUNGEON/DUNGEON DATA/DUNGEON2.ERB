@DUNGEON2_INIT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIMS DYNAMIC MAPDATAS,19

MAPDATAS: 0 '= "0000000000000000000"
MAPDATAS: 1 '= "0310401121011^^^^^0"
MAPDATAS: 2 '= "0312111001114555550"
MAPDATAS: 3 '= "0310101100000111110"
MAPDATAS: 4 '= "0500100121110114110"
MAPDATAS: 5 '= "011[110101110100010"
MAPDATAS: 6 '= "00100101011121040[0"
MAPDATAS: 7 '= "011511^400000001110"
MAPDATAS: 8 '= "0110100003111111000"
MAPDATAS: 9 '= "011514031000100001>"
MAPDATAS:10 '= "01^0^0^010111110110"
MAPDATAS:11 '= "0111111110100010110"
MAPDATAS:12 '= "0050505010110110140"
MAPDATAS:13 '= "0111111010010100200"
MAPDATAS:14 '= "0[00001010411111110"
MAPDATAS:15 '= "0111101[10002001410"
MAPDATAS:16 '= "0041100000011101110"
MAPDATAS:17 '= "0011121111513100000"
MAPDATAS:18 '= "000)00000)000000000"


FOR LCOUNT2,0,19
    FOR LCOUNT,0,19
        IF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == " "
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = -1
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == "["
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = 32
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == "*"
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = 34
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == "^"
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = 24
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == ")"
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = 29
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == ">"
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = 19
        ELSE
            D3D_TOTAL_MAPDATA:2:LCOUNT:LCOUNT2 = TOINT(SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1))
        ENDIF
    NEXT
NEXT

@PLAY_DUNGEON2_BGM
#DIMS DYNAMIC BGMS = "d1.mp3"
SIF EXISTSOUND(BGMS)
    PLAYBGM BGMS
BGM_BEFORE_BATTLE = %BGMS%

@DUNGEON2_ENCOUNTER_SET
{
    IF (DUNGEON_IN_FIELD(1,6,13,17) && !DUNGEON_IN_FIELD(6,,17)) ||
    (DUNGEON_IN_FIELD(9,17,3,6) && !DUNGEON_IN_FIELD(15,,6))
}
    ;두 안전지대
    RETURN 0,0,1
ELSE
    ;디버깅용 전역안전지대 처리 필요
    RETURN 1,7,1
ENDIF

@DUNGEON2_RAND_ENCOUNTER(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
#DIM DYNAMIC LNUM

TFLAG:적무리번호 = 0
TFLAG:적위치2 = 100
TFLAG:적레벨2 = 1
TFLAG:배틀BGM번호 = 1

@DUNGEON2_EVENT(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD

IF DUNGEON_IN_FIELD(9,,18,,XCOORD,YCOORD)
    ;이전 층으로 이동하는 제1경로
    RETURN 9,1,1,1
ELSEIF DUNGEON_IN_FIELD(3,,18,,XCOORD,YCOORD)
    ;이전 층으로 이동하는 제2경로
    RETURN 3,1,1,1
ELSEIF DUNGEON_IN_FIELD(2,,16,,XCOORD,YCOORD)
    ;첫 안전지대 상호작용
    ;동료 캐릭터들과 합류
    PRINTL 공사중.
    WAITANYKEY
    DUNGEON2_SWITCH:0 ++
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(1,,14,,XCOORD,YCOORD)
    IF DUNGEON2_SWITCH:0
        ;첫 안전지대 상호작용을 한 경우
        PRINTL 띵동
        WAITANYKEY
        RETURN XCOORD,YCOORD,1
    ELSE
        ;하지 않은 경우 문을 열지 않는다
        PRINTL 넌 못지나간다
        WAITANYKEY
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF DUNGEON_IN_FIELD(7,,15,,XCOORD,YCOORD)
    ;첫번째 퍼즐 잠긴 문
    IF DUNGEON2_SWITCH:1
        ;클리어한 경우
        DUNGEON_EXTRA_TEXT '= ""
        D3D_CURRENT_MAPDATA:2:10 = 0
        D3D_CURRENT_MAPDATA:4:10 = 0
        D3D_CURRENT_MAPDATA:6:10 = 0
        RETURN XCOORD,YCOORD,1
    ELSEIF !DUNGEON2_FLAG:6
        ;처음 도착한 경우
        D3D_BSW:2:2:10 = 1
        D3D_BSW:2:4:10 = 1
        D3D_BSW:2:6:10 = 1
        DUNGEON2_FLAG:6 ++
        DUNGEON_TIMER_ACTIVATE = 1
        DUNGEON_EXTRA_TEXT '= @"턴 경과, {DUNGEON2_PUZZLE1_SCORE}점 획득"
    ELSEIF DUNGEON2_FLAG:6
        DUNGEON2_FLAG:6 ++
        DUNGEON_TIMER = 0
        DUNGEON2_PUZZLE1_SCORE = 0
        DUNGEON2_PUZZLE1_COUNTER1 = 0
        DUNGEON2_PUZZLE1_COUNTER2 = 0
        DUNGEON2_PUZZLE1_COUNTER3 = 0
        DUNGEON_EXTRA_TEXT '= @"턴 경과, {DUNGEON2_PUZZLE1_SCORE}점 획득"
    ENDIF
    RETURN XCOORD,YCOORD,0
ELSEIF DUNGEON_IN_FIELD(2,,10,,XCOORD,YCOORD)
    ;3번째 과녁
    IF DUNGEON2_FLAG:6
        SELECTCASE DUNGEON2_PUZZLE1_COUNTER3
            CASE 0
                DUNGEON2_PUZZLE1_SCORE += 9
            CASE 1
                DUNGEON2_PUZZLE1_SCORE += 6
            CASE 2
                DUNGEON2_PUZZLE1_SCORE += 1
        ENDSELECT
        DUNGEON2_PUZZLE1_COUNTER3 ++
        SIF DUNGEON2_PUZZLE1_COUNTER3 > 2
            DUNGEON2_PUZZLE1_COUNTER3 = 0
        DUNGEON_TIMER ++
        DUNGEON_EXTRA_TEXT '= @"턴 경과, {DUNGEON2_PUZZLE1_SCORE}점 획득"
    ENDIF
    CALL DUNGEON2_PUZZLE1_CHECK
    RETURN XCOORD,YCOORD,0
ELSEIF DUNGEON_IN_FIELD(4,,10,,XCOORD,YCOORD)
    ;2번째 과녁
    IF DUNGEON2_FLAG:6
        IF !DUNGEON2_PUZZLE1_COUNTER2
            DUNGEON2_PUZZLE1_SCORE *= 2
        ELSE
            DUNGEON2_PUZZLE1_SCORE ++
        ENDIF
        DUNGEON2_PUZZLE1_COUNTER2 ++
        DUNGEON_TIMER ++
        DUNGEON_EXTRA_TEXT '= @"턴 경과, {DUNGEON2_PUZZLE1_SCORE}점 획득"
    ENDIF
    CALL DUNGEON2_PUZZLE1_CHECK
    RETURN XCOORD,YCOORD,0
ELSEIF DUNGEON_IN_FIELD(6,,10,,XCOORD,YCOORD)
    ;1번째 과녁
    IF DUNGEON2_FLAG:6
        SELECTCASE DUNGEON2_PUZZLE1_COUNTER1
            CASE 0
                DUNGEON2_PUZZLE1_SCORE += 7
            CASE 2
                DUNGEON2_PUZZLE1_SCORE += 6
            CASE 4
                DUNGEON2_PUZZLE1_SCORE += 5
            CASEELSE
        ENDSELECT
        DUNGEON2_PUZZLE1_COUNTER1 ++
        DUNGEON_TIMER ++
        DUNGEON_EXTRA_TEXT '= @"턴 경과, {DUNGEON2_PUZZLE1_SCORE}점 획득"
    ENDIF
    CALL DUNGEON2_PUZZLE1_CHECK
    RETURN XCOORD,YCOORD,0
ELSEIF DUNGEON_IN_FIELD(6,,7,,XCOORD,YCOORD)
    ;원거리 상호작용
    DUNGEON2_SWITCH:2 = 1
    D3D_CURRENT_MAPDATA:6:7 = 0
ELSEIF DUNGEON_IN_FIELD(3,,5,,XCOORD,YCOORD)
    IF DUNGEON2_SWITCH:2
        ;문열림
        RETURN XCOORD,YCOORD,1
    ELSE
        ;닫힘
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF DUNGEON_IN_FIELD(4,,1,,XCOORD,YCOORD)
    IF !DUNGEON2_SWITCH:3
        ;첫방문
        PRINTL ...
        WAITANYKEY
        DUNGEON2_SWITCH:3 ++
        RETURN XCOORD,YCOORD,0
    ELSE
        CALL DUNGEON2_EXIT_POINT
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF DUNGEON_IN_FIELD(5,,9,,XCOORD,YCOORD)
    ;바닥 통행가능하게 하는 상호작용
    D3D_CURRENT_MAPDATA:2:12 = 15
    D3D_CURRENT_MAPDATA:4:12 = 15
    D3D_CURRENT_MAPDATA:6:12 = 15
    D3D_CURRENT_MAPDATA:3:7 = 15
    D3D_CURRENT_MAPDATA:3:9 = 15
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(7,,7,,XCOORD,YCOORD)
    ;벽파괴 상호작용
    D3D_CURRENT_MAPDATA:6:7 = 1
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(12,,2,,XCOORD,YCOORD)
    ;서버실 잡담 상호작용
    IF !DUNGEON2_FLAG:7
        DUNGEON2_FLAG:7 ++
    ELSE
    ENDIF
ELSEIF DUNGEON_IN_FIELD(15,,4,,XCOORD,YCOORD)
    ;퍼즐 2
    IF !DUNGEON2_FLAG:8
        DUNGEON2_PUZZLE2_COUNTER = 0,2,1,0,2
        DUNGEON2_FLAG:8 = 1
        CALL DUNGEON2_PUZZLE2_CHECK(5)
        REPEAT 5
            D3D_BSW:2:(COUNT+13):6 = 1
        REND
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF DUNGEON_IN_FIELD(13,17,1,,XCOORD,YCOORD)
    IF DUNGEON2_FLAG:8
        SELECTCASE XCOORD
            CASE 13 TO 17
                CALL DUNGEON2_PUZZLE2_CHECK(XCOORD - 13)
        ENDSELECT
    ENDIF
ELSEIF DUNGEON_IN_FIELD(17,,6,,XCOORD,YCOORD)
    IF DUNGEON2_SWITCH:6
        RETURN XCOORD,YCOORD,1
    ELSE
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF DUNGEON_IN_FIELD(15,,6,,XCOORD,YCOORD)
    ;탈출장치 2
    IF !DUNGEON2_SWITCH:4
        ;첫방문
        PRINTL ...
        WAITANYKEY
        DUNGEON2_SWITCH:4 ++
        RETURN XCOORD,YCOORD,0
    ELSE
        CALL DUNGEON2_EXIT_POINT
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF DUNGEON_IN_FIELD(10,,14,,XCOORD,YCOORD)
    ;벽파괴 상호작용
    D3D_CURRENT_MAPDATA:10:17 = 15
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(16,,15,,XCOORD,YCOORD)
    ;보스전
    PRINTL 보스전 공사중
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(17,,12,,XCOORD,YCOORD)
    ;탈출장치 3
    IF !DUNGEON2_SWITCH:5
        ;첫방문
        PRINTL ...
        WAITANYKEY
        DUNGEON2_SWITCH:5 ++
        RETURN XCOORD,YCOORD,0
    ELSE
        CALL DUNGEON2_EXIT_POINT
        RETURN XCOORD,YCOORD,0
    ENDIF

ELSEIF DUNGEON_IN_FIELD(7,,9,,XCOORD,YCOORD)
    ;첫 보물상자
    PRINTL 보물상자는 텅 비었다! (사실 공사중임)
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(1,,1,,XCOORD,YCOORD)
    ;두번째 보물상자팀 1
    PRINTL 보물상자는 텅 비었다! (사실 공사중임)
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(1,,2,,XCOORD,YCOORD)
    ;두번째 보물상자팀 2
    PRINTL 보물상자는 텅 비었다! (사실 공사중임)
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(1,,3,,XCOORD,YCOORD)
    ;두번째 보물상자팀 3
    PRINTL 보물상자는 텅 비었다! (사실 공사중임)
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(9,,8,,XCOORD,YCOORD)
    ;세번째 보물상자
    PRINTL 보물상자는 텅 비었다! (사실 공사중임)
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ELSEIF DUNGEON_IN_FIELD(12,,17,,XCOORD,YCOORD)
    ;네번째 보물상자
    PRINTL 보물상자는 텅 비었다! (사실 공사중임)
    WAITANYKEY
    RETURN XCOORD,YCOORD,1
ENDIF


@DUNGEON2_PUZZLE1_CHECK
IF DUNGEON_TIMER <= 12 && DUNGEON2_PUZZLE1_SCORE >= 39
    DUNGEON_TIMER = 0
    DUNGEON_TIMER_ACTIVATE = 0
    DUNGEON2_SWITCH:1 = 1
    DUNGEON_EXTRA_TEXT '= "목표를 달성했다!"
    RETURN 1
ELSE
    RETURN 0
ENDIF

@DUNGEON2_PUZZLE2_CHECK(NUMV)
#DIM DYNAMIC NUMV
#DIMS DYNAMIC TEXTA,5,5
TEXTA:0:0 '= "공","일","이","삼","사"
TEXTA:1:0 '= "육","해","공"
TEXTA:2:0 '= "상","중","하"
TEXTA:3:0 '= "천","지","인"
TEXTA:4:0 '= "우","마","차"

SIF NUMV == 5
    GOTO ANCHOR

SELECTCASE NUMV
    CASE 0
        DUNGEON2_PUZZLE2_COUNTER:NUMV ++
        SIF DUNGEON2_PUZZLE2_COUNTER:NUMV > 4
            DUNGEON2_PUZZLE2_COUNTER:NUMV = 0
    CASE 1 TO 4
        DUNGEON2_PUZZLE2_COUNTER:NUMV ++
        SIF DUNGEON2_PUZZLE2_COUNTER:NUMV > 2
            DUNGEON2_PUZZLE2_COUNTER:NUMV = 0
ENDSELECT
$ANCHOR
DUNGEON_EXTRA_TEXT '= ""
REPEAT 5
    DUNGEON_EXTRA_TEXT += TEXTA:COUNT:(DUNGEON2_PUZZLE2_COUNTER:COUNT)
REND

IF DUNGEON_EXTRA_TEXT == "이해하지마"
    DUNGEON_EXTRA_TEXT += " CLEAR"
    DUNGEON2_SWITCH:6 = 1
ENDIF
@DUNGEON2_EXIT_POINT
PRINTL 던전에서 탈출하겠습니까?
PRINTL 이곳에서 재개할 수 있습니다.
PRINTL [0] 예
PRINTL [1] 아니오
DO
    INPUT
    SELECTCASE RESULT
        CASE 0
            CALL DUNGEON_EXIT(2,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
            BREAK
        CASE 1
            BREAK
    ENDSELECT
LOOP 1