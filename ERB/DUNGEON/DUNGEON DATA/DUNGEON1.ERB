@DUNGEON1_INIT
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIMS DYNAMIC MAPDATAS,19

D3D_PLAYER_XCOORD = 9
D3D_PLAYER_YCOORD = 17

MAPDATAS:0  '= "       009000000000"
MAPDATAS:1  '= "       011121101110"
MAPDATAS:2  '= "       011101151^30"
MAPDATAS:3  '= "       011100000000"
MAPDATAS:4  '= "       014101111110"
MAPDATAS:5  '= "       011101020010"
MAPDATAS:6  '= "       00[004011030"
MAPDATAS:7  '= "       041100011000"
MAPDATAS:8  '= "       011103111040"
MAPDATAS:9  '= "       011100001010"
MAPDATAS:10 '= "       0111[1101010"
MAPDATAS:11 '= "       011100111110"
MAPDATAS:12 '= "       011100000000"
MAPDATAS:13 '= "       011100111130"
MAPDATAS:14 '= "       0111[1100000"
MAPDATAS:15 '= "       011100101110"
MAPDATAS:16 '= "       011100101410"
MAPDATAS:17 '= "       011100111110"
MAPDATAS:18 '= "       008000000000"

FOR LCOUNT2,0,19
    FOR LCOUNT,0,19
        IF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == " "
            D3D_TOTAL_MAPDATA:1:LCOUNT:LCOUNT2 = -1
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == "["
            D3D_TOTAL_MAPDATA:1:LCOUNT:LCOUNT2 = 32
        ELSEIF SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1) == "^"
            D3D_TOTAL_MAPDATA:1:LCOUNT:LCOUNT2 = 24
        ELSE
            D3D_TOTAL_MAPDATA:1:LCOUNT:LCOUNT2 = TOINT(SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1))
        ENDIF
    NEXT
NEXT

@PLAY_DUNGEON1_BGM
#DIMS DYNAMIC BGMS = "d1.mp3"
SIF EXISTSOUND(BGMS)
    PLAYBGM BGMS
BGM_BEFORE_BATTLE = %BGMS%

@DUNGEON1_ENCOUNTER_SET
RETURN 1,5
;기본 확률 1 최소 걸음수 5

@DUNGEON1_RAND_ENCOUNTER(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
#DIM DYNAMIC LNUM
IF XCOORD >= 7
    LNUM = RAND:2
    TFLAG:적무리번호 = LNUM
    SELECTCASE LNUM
        CASE 0
            TFLAG:적위치2 = 100
            TFLAG:적레벨2 = 1
        CASE 1
            TFLAG:적위치1 = 100
            TFLAG:적레벨1 = 1
            TFLAG:적위치2 = 100
            TFLAG:적레벨2 = 1
    ENDSELECT
    TFLAG:배틀BGM번호 = 1
;ELSE
ENDIF

;던전 1층에서 일어나는 이벤트
;이벤트 실행 후 통상 처리를 한다면 1
;안한다면 0을 RESULT:2에 반환한다
@DUNGEON1_EVENT(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
IF XCOORD == 9 && YCOORD == 18
    TRYCALL DUNGEON1_EXIT_EVENT
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 9 && YCOORD == 6
    ;중앙 잠긴 문
    IF DUNGEON1_SWITCH:0
        PRINTL 문이 열릴 때 이벤트 미실장
        RETURN XCOORD,YCOORD,1
    ELSE
        TRYCALL DUNGEON1_DOOR1_CLOSED_EVENT
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF XCOORD == 11 && YCOORD == 10
    ;북동쪽 잠긴 작은 방문
    IF DUNGEON1_SWITCH:1
        TRYCALL DUNGEON1_DOOR2_OPEN_EVENT
        RETURN XCOORD,YCOORD,1
    ELSE
        TRYCALL DUNGEON1_DOOR2_CLOSED_EVENT
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF XCOORD == 11 && YCOORD == 14
    ;남동쪽 잠긴 작은 방문
    IF DUNGEON1_SWITCH:2
        TRYCALL DUNGEON1_DOOR3_OPEN_EVENT
        RETURN XCOORD,YCOORD,1
    ELSE
        TRYCALL DUNGEON1_DOOR3_CLOSED_EVENT
        RETURN XCOORD,YCOORD,0
    ENDIF
ELSEIF XCOORD == 8 && YCOORD == 7
    ;잠긴 문 앞 왼쪽에 위치한 상호작용
    TRYCALL DUNGEON1_INTERACT1_EVENT
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 17 && YCOORD == 8
    ;북쪽 작은 방의 첫 상호작용
    TRYCALL DUNGEON1_INTERACT2_EVENT
    TFLAG:적위치1 = 100
    TFLAG:적레벨1 = 3
    TFLAG:적위치2 = 100
    TFLAG:적레벨2 = 3
    TFLAG:적위치3 = 100
    TFLAG:적레벨3 = 3
    TFLAG:도주방지 = 1
    TFLAG:배틀BGM번호 = 1
    CALL BATTLE_START
    TRYCALL DUNGEON1_INTERACT2_EVENT_AFTER
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 12 && YCOORD == 6
    ;북쪽 작은 방의 두번째 상호작용
    TRYCALL DUNGEON1_INTERACT3_EVENT
    RETURN XCOORD,YCOORD,0
ELSEIF XCOORD == 16 && YCOORD == 16
    ;남쪽 작은 방 상호작용
    TRYCALL DUNGEON1_INTERACT4_EVENT
    RETURN XCOORD,YCOORD,0
ELSEIF XCOORD == 13 && YCOORD == 2
    ;중앙 잠긴 문 너머 작은 방 상호작용
    TRYCALL DUNGEON1_INTERACT5_EVENT
    RETURN XCOORD,YCOORD,0
ELSEIF XCOORD == 12 && YCOORD == 8
    ;북동쪽 작은방 첫 보물상자
    ITEM:한결같이내일을향해 += 1
    PRINTL 장비 아이템 한결같이 내일을 향해를 1개 얻었다.
    WAITANYKEY
    TRYCALL DUNGEON1_TREASURE1_EVENT
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 17 && YCOORD == 6
    ;북동쪽 작은방 두번째 보물상자
    ITEM:스파크드링크20 += 3
    PRINTL 스파크 드링크 20을 3개 얻었다.
    WAITANYKEY
    TRYCALL DUNGEON1_TREASURE2_EVENT
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 17 && YCOORD == 13
    ;남동쪽 작은방 보물상자
    ITEM:감응형스마트섬유 += 4
    PRINTL 감응형스마트섬유를 4개 얻었다.
    WAITANYKEY
    TRYCALL DUNGEON1_TREASURE3_EVENT
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 17 && YCOORD == 2
    ;중앙 잠긴 문 너머 퍼즐 뒤쪽 보물상자
    ITEM:붉은체크무늬원단 += 1
    PRINTL 붉은체크무늬원단을 1개 얻었다.
    TRYCALL DUNGEON1_TREASURE4_EVENT
    RETURN XCOORD,YCOORD,1
ELSEIF XCOORD == 16 && YCOORD == 2
    IF DUNGEON1_FLAG:5
        ;원거리 상호작용 튜토리얼 용 표적
        TRYCALL DUNGEON1_RANGE1_EVENT
        ;맵 열림 처리
        TRYCALL DUNGEON1_RANGE1_EVENT_AFTER
        RETURN XCOORD,YCOORD,1
        ;반환 결과로 0으로 바꿔야한다.
    ELSE
        RETURN XCOORD,YCOORD,0
    ENDIF
ENDIF