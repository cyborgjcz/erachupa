;디버그용 공간이동 치트 식중함수
;DEBUG_W(N,M)이라 콘솔에 입력하여 해당 좌표로 이동
@DEBUG_W(XV,YV)
#FUNCTION
#DIM DYNAMIC XV
#DIM DYNAMIC YV
D3D_PLAYER_XCOORD = XV
D3D_PLAYER_YCOORD = YV
RETURNF 1

;던전입장시 실행하는 함수, 루프는 INDUNGEON 변수 조작으로 탈출
@DUNGEON_START(DUNGEONNUM)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC DUNGEONNUM
#DIM DYNAMIC INITEVENT
;지도 데이터 갱신
FOR LCOUNT2,0,19
    FOR LCOUNT,0,19
        D3D_CURRENT_MAPDATA:LCOUNT:LCOUNT2 = D3D_TOTAL_MAPDATA:DUNGEONNUM:LCOUNT:LCOUNT2
    NEXT
NEXT

;방향 자동전환
CALL D3D_AUTOFLIP
;전장의 안개 표시
CALL D3D_BLACKSHEEPWALL(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)

INDUNGEON = 1
CALLFORM PLAY_DUNGEON{DUNGEONNUM}_BGM
DO
    CALL D3D_SHOW_GRAPHIC
    IF !GIANT_BATTLE
        CALL D3D_SHOW_COMMAND
        IF INITEVENT != 1;(매번) 던전 입장 시(마다) 실행하는 이벤트
            CALL DUNGEON_INIT_EVENT(DUNGEONNUM)
            INITEVENT = 1
        ENDIF
        CALL D3D_INPUT_COMMAND
        SIF RESULT
            CALL DUNGEON_RAND_ENCOUNTER
    ELSE
        ;거대 보스 전투
        CALL COMMAND_GIANT_BATTLE
    ENDIF
LOOP INDUNGEON
STOPBGM

;던전 내 상호작용
@DUNGEON_INTERACT
CALL D3D_GET_FRONT_COORD(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
;바로앞 상호작용
IF IS_ABLE_TO_INTERACT(RESULT:0,RESULT:1) && !(RESULT:0 == -1 || RESULT:1 == -1 || RESULT:0 == 20 || RESULT:1 == 20)
    RESULT:2 = 0
    SELECTCASE D3D_CURRENT_MAPDATA:(RESULT:0):(RESULT:1)
        CASE 2;문
            TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
            CATCH
                DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
            ENDCATCH
            SIF RESULT:2 == 1
                CALL DUNGEON_OPEN_DOOR(RESULT:0,RESULT:1)
        CASE 32;잠긴 문
            TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
            CATCH
                DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
            ENDCATCH
            SIF RESULT:2 == 1
                CALL DUNGEON_OPEN_DOOR(RESULT:0,RESULT:1)
        CASE 3;보물상자
            TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
            CATCH
                DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
            ENDCATCH
            SIF RESULT:2 == 1
                D3D_CURRENT_MAPDATA:(RESULT:0):(RESULT:1) = 13
        CASE 4;통상 이벤트
            TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
            CATCH
                DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
            ENDCATCH
            SIF RESULT:2 == 1
                D3D_CURRENT_MAPDATA:(RESULT:0):(RESULT:1) = 14
        CASE 8;출구
            TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
            CATCH
                DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
            ENDCATCH
            SIF RESULT:2 == 1
                CALL DUNGEON_EXIT(FLAG:탐색대상던전,RESULT:0,RESULT:1)
        CASE 9,19,29,39;
            TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
            CATCH
                DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
            ENDCATCH
            SIF RESULT:2 == 1
                CALL DUNGEON_TRANSFER(RESULT:0,RESULT:1,RESULT:3)
            ;워프 함수는 각각 대상 좌표 2개, 실행여부, 층수를 반환한다
    ENDSELECT
ELSE
    ;발밑
    IF IS_ABLE_TO_INTERACT(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
        RESULT:2 = 0
        SELECTCASE D3D_CURRENT_MAPDATA:D3D_PLAYER_XCOORD:D3D_PLAYER_YCOORD
            CASE 4;통상 이벤트
                TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD
                CATCH
                    DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({D3D_PLAYER_XCOORD},{D3D_PLAYER_YCOORD}) 호출 실패
                ENDCATCH
                SIF RESULT:2 == 1
                    D3D_CURRENT_MAPDATA:(RESULT:0):(RESULT:1) = 14
            CASE 6;올라가는 계단
                TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD
                CATCH
                    DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({D3D_PLAYER_XCOORD},{D3D_PLAYER_YCOORD}) 호출 실패
                ENDCATCH
                SIF RESULT:2 == 1
                    CALL DUNGEON_STAIR(FLAG:탐색대상던전,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD,6)
            CASE 7;내려가는 계단
                TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD
                CATCH
                    DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({D3D_PLAYER_XCOORD},{D3D_PLAYER_YCOORD}) 호출 실패
                ENDCATCH
                SIF RESULT:2 == 1
                    CALL DUNGEON_STAIR(FLAG:탐색대상던전,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD,7)
            CASE 8;출구
                TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD
                CATCH
                    DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({D3D_PLAYER_XCOORD},{D3D_PLAYER_YCOORD}) 호출 실패
                ENDCATCH
                SIF RESULT:2 == 1
                    CALL DUNGEON_EXIT(FLAG:탐색대상던전,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
            CASE 9,19,29,39;
                TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,RESULT:0,RESULT:1
                CATCH
                    DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_EVENT({RESULT:0},{RESULT:1}) 호출 실패
                ENDCATCH
                SIF RESULT:2 == 1
                    CALL DUNGEON_TRANSFER(RESULT:0,RESULT:1,RESULT:2)
        ENDSELECT
    ENDIF
ENDIF
RETURN 0;D3D_INPUT_COMMAND 함수에 전달하는 값. 0은 턴을 증가시키지 않는다.

;플레이어 위치,방향에서 해당 위치의 상호작용이 가능한지 검사한다.
@IS_ABLE_TO_INTERACT(XCOORD=-1,YCOORD=-1)
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
#DIM DYNAMIC T_BUTTON = 0;TARGET_BUTTON
#DIM DYNAMIC T_COORD,2
SIF XCOORD < 0
    XCOORD = D3D_PLAYER_XCOORD
SIF YCOORD < 0
    YCOORD = D3D_PLAYER_YCOORD

;발밑의 경우
IF XCOORD == D3D_PLAYER_XCOORD && YCOORD == D3D_PLAYER_YCOORD
    LCOUNT = D3D_CURRENT_MAPDATA:XCOORD:YCOORD
    SELECTCASE LCOUNT % 10
        CASE 4,6,7,8,9,19,29,39;통상 이벤트, 계단,출구,화살표
            IF LCOUNT < 10 || LCOUNT > 19
                RETURNF 1
            ENDIF
    ENDSELECT
ENDIF

SELECTCASE D3D_PLAYER_DIRECTION
    CASE 0
        T_COORD:0 = D3D_PLAYER_XCOORD
        T_COORD:1 = D3D_PLAYER_YCOORD - 1
    CASE 1
        T_COORD:0 = D3D_PLAYER_XCOORD + 1
        T_COORD:1 = D3D_PLAYER_YCOORD
    CASE 2
        T_COORD:0 = D3D_PLAYER_XCOORD
        T_COORD:1 = D3D_PLAYER_YCOORD + 1
    CASE 3
        T_COORD:0 = D3D_PLAYER_XCOORD - 1
        T_COORD:1 = D3D_PLAYER_YCOORD
ENDSELECT
;바로 앞의 경우
{
    IF (XCOORD == T_COORD:0 && YCOORD == T_COORD:1) &&
    !(XCOORD < 0 || YCOORD < 0 || XCOORD >= D3D_CURRENT_MAPXSIZE || YCOORD >= D3D_CURRENT_MAPYSIZE)
}
    LCOUNT = D3D_CURRENT_MAPDATA:XCOORD:YCOORD
    SELECTCASE LCOUNT % 10
        CASE 2,32,3,4,24,8,9,19,29,39;문,(잠긴문),보물상자,통상이벤트,(원격가이벤트),출구,화살표
            IF LCOUNT < 10 || LCOUNT > 19
                RETURNF 1
            ENDIF
    ENDSELECT
ENDIF

;그 밖의 경우
;시야에 들어오는가? 식별 가능한가?
FOR LCOUNT,0,39
    IF XCOORD == D3D_TEXTBUTTONCOORD:LCOUNT:0 && YCOORD == D3D_TEXTBUTTONCOORD:LCOUNT:1
        T_BUTTON = D3D_TEXTBUTTON:LCOUNT
    ENDIF
NEXT

IF T_BUTTON == 1 && D3D_CURRENT_MAPDATA:XCOORD:YCOORD >= 20 && D3D_CURRENT_MAPDATA:XCOORD:YCOORD < 30
    RETURNF 1
ENDIF
RETURNF 0

;문따기
@DUNGEON_OPEN_DOOR(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD

D3D_CURRENT_MAPDATA:XCOORD:YCOORD = 12
RETURN 1

@DUNGEON_STAIR(DUNGEONV,XCOORD,YCOORD,WHERE=6)
#DIM DYNAMIC DUNGEONV
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
#DIM DYNAMIC WHERE;6or7
DEBUGPRINTFORML 계단 미실장 인수={DUNGEONV}, {XCOORD}, {YCOORD}, {WHERE}

;출구
@DUNGEON_EXIT(DUNGEONV,XCOORD,YCOORD)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC DUNGEONV;현재탐색던전층
#DIM DYNAMIC XCOORD;좌표
#DIM DYNAMIC YCOORD

FOR LCOUNT2,0,19
    FOR LCOUNT,0,19
        D3D_TOTAL_MAPDATA:DUNGEONV:LCOUNT:LCOUNT2 = D3D_CURRENT_MAPDATA:LCOUNT:LCOUNT2
    NEXT
NEXT
FOR LCOUNT,1,3
    CALL DETRANSFORM(FLAG:LCOUNT)
NEXT
INDUNGEON = 0

;던전 층간 이동
@DUNGEON_TRANSFER(XCOORD,YCOORD,DUNGEONV)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
#DIM DYNAMIC DUNGEONV

FOR LCOUNT2,0,19
    FOR LCOUNT,0,19
        D3D_TOTAL_MAPDATA:(FLAG:탐색대상던전):LCOUNT:LCOUNT2 = D3D_CURRENT_MAPDATA:LCOUNT:LCOUNT2
    NEXT
NEXT
FOR LCOUNT2,0,19
    FOR LCOUNT,0,19
        D3D_CURRENT_MAPDATA:LCOUNT:LCOUNT2 = D3D_TOTAL_MAPDATA:DUNGEONV:LCOUNT:LCOUNT2
    NEXT
NEXT
D3D_PLAYER_XCOORD = XCOORD
D3D_PLAYER_YCOORD = YCOORD
FLAG:탐색대상던전 = DUNGEONV
CALL D3D_BLACKSHEEPWALL(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)

;(매번) 던전 탐색 시작 시 실행하는 이벤트를 호출
@DUNGEON_INIT_EVENT(DUNGEONV)
#DIM DYNAMIC DUNGEONV
TRYCALLFORM DUNGEON{DUNGEONV}_INIT_EVENT

;원거리 상호작용 이벤트 호출
;버튼 존재 유무는 D3D_INPUT_COMMAND에서 처리 완료
@DUNGEON_FAR_EVENT(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD

RESULT:2 = 0
TRYCALLFORM DUNGEON{FLAG:탐색대상던전}_EVENT,XCOORD,YCOORD
IF RESULT:2 == 1
    SELECTCASE D3D_CURRENT_MAPDATA:XCOORD:YCOORD
        CASE 24
            D3D_CURRENT_MAPDATA:XCOORD:YCOORD = 0
        CASE 34
            D3D_CURRENT_MAPDATA:XCOORD:YCOORD = 1
    ENDSELECT
ENDIF

;던전 랜덤 인카운터 전투 구현
@DUNGEON_RAND_ENCOUNTER
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC CHANCE
#DIM DYNAMIC LNUM

SIF !ALLOW_RAND_ENCOUNTER()
    RETURN 0

VARSET RESULT
TRYCALLFORM DUNGEON{FLAG:탐색대상던전}_ENCOUNTER_SET
;2번 반환값은 무조건적으로 인카운터 없음 처리(안전지대)
SIF RESULT:2
    RETURN 0
;매 걸음수마다 증가하는 수치가 위 함수의 반환값보다 적다면...
SIF ENCOUNTER_TURN < RESULT:1
    RETURN 0
;확률은 기본 확률 + 수정치 / 100
CHANCE = RESULT:0 + ENCOUNTER_MODIFIER
LNUM = RAND:100

IF LNUM <= CHANCE
    DEBUGPRINTFORML @DUNGEON_RAND_ENCOUNTER 전투 돌입 확률은 {CHANCE} / 100 주사위 눈은 {LNUM}이었습니다.
    ;전투 실행하는 경우
    FOR LCOUNT,1,7
        VARSET TFLAG:LCOUNT
        VARSET TFLAG:(LCOUNT+10)
    NEXT
    TRYCCALLFORM DUNGEON{FLAG:탐색대상던전}_RAND_ENCOUNTER,D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD
    CATCH
        DEBUGPRINTFORML DUNGEON{FLAG:탐색대상던전}_RAND_ENCOUNTER,{D3D_PLAYER_XCOORD},{D3D_PLAYER_YCOORD} 호출실패
    ENDCATCH
    CALL BATTLE_START
    ENCOUNTER_TURN = 0
    ENCOUNTER_MODIFIER = 0
    SIF TFLAG:타이틀이행 != 0
        GOTO RANDENCOUNTER_GOTITLE
    RETURN 1
ELSE
    ENCOUNTER_MODIFIER += 10
    RETURN 0
ENDIF

$RANDENCOUNTER_GOTITLE
RESETDATA
BEGIN TITLE

;좌표가 던전 내 2차원 평면 범위 내에 있는지 판정하는 함수
@DUNGEON_IN_FIELD(XL,XR=-1,YH,YL=-1,XCOORD=-1,YCOORD=-1)
#FUNCTION
#DIM DYNAMIC XL
#DIM DYNAMIC XR
#DIM DYNAMIC YH
#DIM DYNAMIC YL
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD

;X와 Y의 각 두번째 인자는 생략할 경우 한 점의 영역을 가리키도록 초기화
SIF XR == -1
    XR = XL
SIF YL == -1
    YL = YH
;5,6번은 전달받지 않으면 자동으로 플레이어 좌표로
SIF XCOORD == -1
    XCOORD = D3D_PLAYER_XCOORD
SIF YCOORD == -1
    YCOORD = D3D_PLAYER_YCOORD

SELECTCASE XCOORD
    CASE XL TO XR
        SELECTCASE YCOORD
            CASE YH TO YL
                RETURNF 1
            CASEELSE
                RETURNF 0
        ENDSELECT
    CASEELSE
        RETURNF 0
ENDSELECT

;던전 랜덤 인카운터 허용 여부
@ALLOW_RAND_ENCOUNTER
#FUNCTION
SIF GIANT_BATTLE != 0
    RETURNF 0
RETURNF 1

;DUNGEON_INIT 일괄실행
@SET_ALL_DUNGEON
#DIM DYNAMIC LCOUNT
FOR LCOUNT,1,DUNGEONFLOORNUM
    TRYCALLFORM DUNGEON{LCOUNT}_INIT
NEXT