;현재 인수를 사용하지 않음
@DUNGEON_START(DUNGEONNUM)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC DUNGEONNUM

;이하 테스트용 맵데이터 하드코딩
;[SKIPSTART]
#DIMS DYNAMIC MAPDATAS,6
D3D_CURRENT_MAPXSIZE = 6
D3D_CURRENT_MAPYSIZE = 6

MAPDATAS:0 '= "000000"
MAPDATAS:1 '= "011000"
MAPDATAS:2 '= "011210"
MAPDATAS:3 '= "011010"
MAPDATAS:4 '= "000000"
MAPDATAS:5 '= "000000"

FOR LCOUNT2,0,6
    FOR LCOUNT,0,6
        D3D_CURRENT_MAPDATA:LCOUNT:LCOUNT2 = TOINT(SUBSTRING(MAPDATAS:LCOUNT2,LCOUNT,1))
    NEXT
NEXT

D3D_PLAYER_XCOORD = 2
D3D_PLAYER_YCOORD = 2
D3D_PLAYER_DIRECTION = 3
;[SKIPEND]

DO
    CALL D3D_SHOW_GRAPHIC
    CALL D3D_SHOW_COMMAND
    CALL D3D_INPUT_COMMAND
;SIF RESULT:0
;    턴수 += 1
LOOP 1

@DUNGEON_INTERACT
CALL D3D_GET_FRONT_COORD(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
IF IS_ABLE_TO_INTERACT(RESULT:0,RESULT:1)
    SELECTCASE D3D_CURRENT_MAPDATA:(RESULT:0):(RESULT:1)
        CASE 2;문
            CALL DUNGEON_OPEN_DOOR(RESULT:0,RESULT:1)
        CASE 3;보물상자
        CASE 4;통상 이벤트
        CASE 8;출구
    ENDSELECT
ELSE
    IF IS_ABLE_TO_INTERACT(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
        SELECTCASE D3D_CURRENT_MAPDATA:D3D_PLAYER_XCOORD:D3D_PLAYER_YCOORD
            CASE 4;통상 이벤트
            CASE 6;보물상자
            CASE 7;통상 이벤트
            CASE 8;출구
        ENDSELECT
        DEBUGPRINTFORML 발밑 상호작용 가능! {D3D_PLAYER_XCOORD},{D3D_PLAYER_YCOORD}
    ENDIF
ENDIF
;플레이어 위치,방향에서 해당 위치의 상호작용이 가능한지 검사한다.
@IS_ABLE_TO_INTERACT(XCOORD=-1,YCOORD=-1)
#FUNCTION
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD
#DIM DYNAMIC T_BUTTON = 0;TARGET_BUTTON
#DIM DYNAMIC T_COORD,2
SIF XCOORD < 0
    XCOORD = D3D_PLAYER_XCOORD
SIF YCOORD < 0
    YCOORD = D3D_PLAYER_YCOORD

;발밑의 경우
IF XCOORD == D3D_PLAYER_XCOORD && YCOORD == D3D_PLAYER_YCOORD
    LCOUNT = D3D_CURRENT_MAPDATA:XCOORD:YCOORD
    SELECTCASE LCOUNT % 10
        CASE 4,6,7,8;통상 이벤트, 계단,출구
            IF LCOUNT < 10 || LCOUNT > 19
                DEBUGPRINTFORML LCOUNT {LCOUNT}
                RETURNF 1
            ENDIF
    ENDSELECT
ENDIF

SELECTCASE D3D_PLAYER_DIRECTION
    CASE 0
        T_COORD:0 = D3D_PLAYER_XCOORD
        T_COORD:1 = D3D_PLAYER_YCOORD - 1
    CASE 1
        T_COORD:0 = D3D_PLAYER_XCOORD + 1
        T_COORD:1 = D3D_PLAYER_YCOORD
    CASE 2
        T_COORD:0 = D3D_PLAYER_XCOORD
        T_COORD:1 = D3D_PLAYER_YCOORD + 1
    CASE 3
        T_COORD:0 = D3D_PLAYER_XCOORD - 1
        T_COORD:1 = D3D_PLAYER_YCOORD
ENDSELECT
;바로 앞의 경우
IF XCOORD == T_COORD:0 && YCOORD == T_COORD:1
    LCOUNT = D3D_CURRENT_MAPDATA:XCOORD:YCOORD
    SELECTCASE LCOUNT % 10
        CASE 2,3,4,8;문,보물상자,통상이벤트,출구
            IF LCOUNT < 10 || LCOUNT > 19
                DEBUGPRINTFORML LCOUNT {LCOUNT}
                RETURNF 1
            ENDIF
    ENDSELECT
ENDIF

;그 밖의 경우
;시야에 들어오는가? 식별 가능한가?
FOR LCOUNT,0,39
    IF XCOORD == D3D_TEXTBUTTONCOORD:LCOUNT:0 && YCOORD == D3D_TEXTBUTTONCOORD:LCOUNT:1
        T_BUTTON = D3D_TEXTBUTTON:LCOUNT
    ENDIF
NEXT

IF T_BUTTON == 1 && D3D_CURRENT_MAPDATA:XCOORD:YCOORD >= 20 && D3D_CURRENT_MAPDATA:XCOORD:YCOORD < 30
    DEBUGPRINTFORML MAPDATA = {D3D_CURRENT_MAPDATA:XCOORD:YCOORD}
    RETURNF 1
ENDIF
RETURNF 0

@DUNGEON_OPEN_DOOR(XCOORD,YCOORD)
#DIM DYNAMIC XCOORD
#DIM DYNAMIC YCOORD

SIF D3D_CURRENT_MAPDATA:XCOORD:YCOORD != 2
    RETURN 0
D3D_CURRENT_MAPDATA:XCOORD:YCOORD = 12
RETURN 1