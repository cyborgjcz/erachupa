;
;D3D_SHOW_COMMAND
;유사3D 화면 밑에 커맨드 표시
;
@D3D_SHOW_COMMAND
#DIM DYNAMIC PETIT_STAT_DIST = 1820
PRINT_SPACE D3D_LEFT_SPACE
PRINTBUTTON "［ｑ］←","q"
PRINTBUTTON "［ｗ］↑","w"
PRINTBUTTON "［ｅ］→","e"
PRINT_SPACE 400
PRINTBUTTON "［ｉ］아이템","i"
PRINT_SPACE (-50) + PETIT_STAT_DIST
CALL SHOW_PETIT_STAT(0)
PRINTL
PRINT_SPACE D3D_LEFT_SPACE
PRINTBUTTON "［ａ］↶","a"
PRINT_SPACE 40
PRINTBUTTON "［ｓ］↓","s"
PRINTBUTTON "［ｄ］↷","d"
PRINT_SPACE 1020 + PETIT_STAT_DIST
CALL SHOW_PETIT_STAT(1)
PRINTL
PRINT_SPACE D3D_LEFT_SPACE
PRINTBUTTON "［ｚ］상호작용","z"
PRINT_SPACE 1460 + PETIT_STAT_DIST
CALL SHOW_PETIT_STAT(2)

;던전 스탯 표시 화면
@SHOW_PETIT_STAT(LINEV)
#DIM DYNAMIC LINEV
SELECTCASE LINEV
    CASE 0
        PRINT_SPACE IMAGE_MAG * 50
        STRLENSU CALLNAME:(FLAG:1);FLAG:1~2 는 동료1 동료2
        PRINTFORM %CALLNAME:(FLAG:1)%
        PRINT_SPACE 100*(9 - RESULT)
        PRINT_SPACE IMAGE_MAG * 50 + 50
        STRLENSU CALLNAME:(FLAG:2);FLAG:1~2 는 동료1 동료2  
        PRINTFORM %CALLNAME:(FLAG:2)%
        PRINT_SPACE 100*(8 - RESULT)
    CASE 1
        PRINT_SPACE IMAGE_MAG * 50
        PRINTFORM ＨＰ%TOFULL(TOSTR(BASE:(FLAG:1):희망)),6,RIGHT%／%TOFULL(TOSTR(MAXBASE:(FLAG:1):희망)),6,RIGHT%
        PRINT_SPACE IMAGE_MAG * 50 + 50
        PRINTFORM ＨＰ%TOFULL(TOSTR(BASE:(FLAG:2):희망)),6,RIGHT%／%TOFULL(TOSTR(MAXBASE:(FLAG:2):희망)),6,RIGHT%
    CASE 2
        PRINT_SPACE IMAGE_MAG * 50
        PRINTFORM ＳＰ%TOFULL(TOSTR(BASE:(FLAG:1):영혼)),6,RIGHT%／%TOFULL(TOSTR(MAXBASE:(FLAG:1):영혼)),6,RIGHT%
        PRINT_SPACE IMAGE_MAG * 50 + 50
        PRINTFORM ＳＰ%TOFULL(TOSTR(BASE:(FLAG:2):영혼)),6,RIGHT%／%TOFULL(TOSTR(MAXBASE:(FLAG:2):영혼)),6,RIGHT%
        HTML_PRINT @"<nonbutton pos='{D3D_LEFT_SPACE+3980}'><p align='left'><nobr><img src='%FACE(FLAG:1,1)%' height='{IMAGE_MAG * 50}' width='{IMAGE_MAG * 50}' ypos='-{(IMAGE_MAG * 50)}'></nonbutton><nonbutton pos='{D3D_LEFT_SPACE+5220}'><img src='%FACE(FLAG:2,1)%' height='{IMAGE_MAG * 50}' width='{IMAGE_MAG * 50}' ypos='-{(IMAGE_MAG * 50)}'></nonbutton>"
ENDSELECT
;
;D3D_INPUT_COMMAND
;커맨드 처리
;
@D3D_INPUT_COMMAND
#DIM DYNAMIC KEYV
ONEINPUTS

KEYV = FINDELEMENT(D3D_TEXTBUTTONSYMBOL,RESULTS)
IF KEYV != -1
    IF D3D_TEXTBUTTON:KEYV
        CALL DUNGEON_FAR_EVENT(D3D_TEXTBUTTONCOORD:KEYV:0,D3D_TEXTBUTTONCOORD:KEYV:1)
    ENDIF
ELSE
    SELECTCASE RESULTS
        CASE "s"
            CALL D3D_MOVE_PLAYER(2)
        CASE "e"
            CALL D3D_MOVE_PLAYER(1)
        CASE "q"
            CALL D3D_MOVE_PLAYER(3)
        CASE "a"
            CALL D3D_CHANGE_DIRECTION(-1)
            RETURN 0
        CASE "w"
            CALL D3D_MOVE_PLAYER(0)
        CASE "d"
            CALL D3D_CHANGE_DIRECTION(1)
            RETURN 0
        CASE "z"
            CALL DUNGEON_INTERACT
            RESULT = 0
        CASE "i"
            CALL ITEM_CONSUME_UI
            RESULT = 0
        CASE ""
            RESTART
        CASEELSE
            CLEARLINE 1
            REUSELASTLINE
            RESTART
    ENDSELECT
ENDIF
IF !RESULT
    RETURN 0
ELSE
    RETURN 1
ENDIF