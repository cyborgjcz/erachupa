;적 1체를 공격하는 경우
@SKILL_SINGLE_ATTACK(TARGETV,USERV,SKILLV,ISENEMY=0,WIDE=0)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LNUM
#DIM DYNAMIC TARGETV
#DIM DYNAMIC USERV
#DIM DYNAMIC SKILLV
#DIM DYNAMIC DAMAGEV
#DIM DYNAMIC ISENEMY;적이 공격하고 있는 경우
#DIM DYNAMIC WIDE

#DIM DYNAMIC BASUTECHECK;bit연산으로 거는 상태이상 체크
#DIM DYNAMIC BASUTECHANCE,5;상태이상 확률 기록
#DIM DYNAMIC BASUTESUCCESS,5;상태이상 성공여부?
#DIM DYNAMIC RESISTV;상태이상 저항 값
IF !ISENEMY
    CFLAG:(FLAG:USERV):전회공격상대 = TARGETV
    CFLAG:(FLAG:USERV):전회보조행동 = 0
    SIF !WIDE
        CALL BATTLE_TALK("SKILL",USERV,,SKILLV)
    IF !RESULT && !WIDE
        PRINTFORML %조사처리(CALLNAME:(FLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
        PRINT ...
        WAITANYKEY
    ENDIF
    ;1~6로 전달되는 TARGETV를 실제 값으로 변환
    TARGETV = TFLAG:TARGETV
ELSEIF ISENEMY
    CFLAG:(TFLAG:USERV):전회공격상대 = TARGETV
    CFLAG:(TFLAG:USERV):전회보조행동 = 0
    ;CALL ENEMY_BATTLETALK("SKILL",NO:USERV,,SKILLV)
    ;SIF !RESULT
    IF !WIDE
        PRINTFORML ({USERV}) %조사처리(CALLNAME:(TFLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
        PRINT ...
        WAITANYKEY
    ENDIF
    TARGETV = FLAG:TARGETV
ENDIF
IF RAND:100 <= (100-SKILL_DATA:SKILLV:8)+(BASE:TARGETV:회피율)-(SKILL_DATA:SKILLV:9)
    ;MISS
    SIF WIDE
        PRINTFORM %조사처리(CALLNAME:TARGETV,"을")% 노린 공격은 
    PRINTL 빗나갔다!
    SIF !ISENEMY && !WIDE
        CALL BATTLE_TALK("MISS",USERV)
    ;ELSEIF ISENEMY
    WAITANYKEY
ELSE
    IF !ISENEMY
        USERV = FLAG:USERV
    ELSEIF ISENEMY
        USERV = TFLAG:USERV
    ENDIF
    SELECTCASE SKILL_DATA:SKILLV:6
        CASE 0
            ;공격력 기반
            DAMAGEV = ((BASE:USERV:공격력) * (120-RAND:40)) / 100
            DAMAGEV = SKILL_CALC_ARMOR(TARGETV,SKILLV,DAMAGEV)
        CASE 1,2,3
            ;패기/예리/자비 대미지는 스탯 제곱근 * 베이스값 또한 난수에 의한 변동폭이 작다
            DAMAGEV = ((SKILL_DATA:SKILLV:4 * SQRT(BASE:USERV:(1+SKILL_DATA:SKILLV:6)) * (107-RAND:14))) / 100
            DAMAGEV = SKILL_CALC_ARMOR(TARGETV,SKILLV,DAMAGEV)
        CASE 4
            ;고정값
            DAMAGEV = SKILL_DATA:SKILLV:4
            DAMAGEV = SKILL_CALC_ARMOR(TARGETV,SKILLV,DAMAGEV)
    ENDSELECT
    ;격앙처리
    SIF (CFLAG:USERV:상태이상 & 16) != 0
        TIMES DAMAGEV, 1.20
    SIF (CFLAG:USERV:상태이상 & 16) != 0
        TIMES DAMAGEV, 1.20
    ;HP 감소
    BASE:TARGETV:희망 -= DAMAGEV
    ;상태이상부여
    FOR LCOUNT,12,16,2
        IF SKILL_DATA:SKILLV:LCOUNT != 0
            SELECTCASE SKILL_DATA:SKILLV:LCOUNT
                CASE 1;즉사부여
                    IF (BASUTECHECK & 1) == 0
                        BASUTECHECK += 1
                        BASUTECHANCE:0 = SKILL_DATA:SKILLV:(LCOUNT+1)
                    ENDIF
                CASE 2;독부여
                    IF (BASUTECHECK & 2) == 0
                        BASUTECHECK += 2
                        BASUTECHANCE:1 = SKILL_DATA:SKILLV:(LCOUNT+1)
                    ENDIF
                CASE 3;출혈부여
                    IF (BASUTECHECK & 4) == 0
                        BASUTECHECK += 4
                        BASUTECHANCE:2 = SKILL_DATA:SKILLV:(LCOUNT+1)
                    ENDIF
                CASE 4;공포부여
                    IF (BASUTECHECK & 8) == 0
                        BASUTECHECK += 8
                        BASUTECHANCE:3 = SKILL_DATA:SKILLV:(LCOUNT+1)
                    ENDIF
                CASE 5;격앙부여
                    IF (BASUTECHECK & 16) == 0
                        BASUTECHECK += 16
                        BASUTECHANCE:4 = SKILL_DATA:SKILLV:(LCOUNT+1)
                    ENDIF
            ENDSELECT
        ENDIF
    NEXT
    FOR LCOUNT,0,5
        IF LCOUNT == 0
            LNUM = 1
        ELSE
            LNUM = POWER(2,LCOUNT)
        ENDIF
        IF (BASUTECHECK & LNUM) != 0
            ;상태이상판정
            RESISTV = 0
            IF LCOUNT == 1
                RESISTV = 20 * BASE:TARGETV:독내성
            ELSEIF LCOUNT == 2
                RESISTV = 20 * BASE:TARGETV:출혈내성
            ELSEIF LCOUNT == 3
                RESISTV = 20 * BASE:TARGETV:공포내성
            ELSEIF LCOUNT == 4
                RESISTV = 20 * BASE:TARGETV:격앙내성
            ENDIF
            IF (RAND:100 + RESISTV) <= BASUTECHANCE:LCOUNT
                BASUTESUCCESS:LCOUNT = 1
                IF (CFLAG:TARGETV:상태이상 & LNUM) != 0
                    BASUTESUCCESS:LCOUNT = 2
                ELSE
                    ;여기서 실제로 상태이상 부여
                    CFLAG:TARGETV:상태이상 += LNUM
                    ;상태이상 지속시간 부여
                    SELECTCASE LCOUNT
                        CASE 1
                            상태이상시간:TARGETV:LCOUNT = (8 - (2 * BASE:TARGETV:독내성))
                        CASE 2
                            상태이상시간:TARGETV:LCOUNT = (8 - (2 * BASE:TARGETV:출혈내성))
                        CASE 3
                            상태이상시간:TARGETV:LCOUNT = (6 - (BASE:TARGETV:공포내성))
                        CASE 4
                            상태이상시간:TARGETV:LCOUNT = (6 - (BASE:TARGETV:격앙내성))
                    ENDSELECT
                ENDIF
            ENDIF
        ENDIF
    NEXT
    IF DAMAGEV != 0
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% {DAMAGEV} 피해를 받았다!
    ENDIF
    SIF BASUTESUCCESS:0 == 2
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 이미 쓰러져있다.
    SIF BASUTESUCCESS:0 == 1
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 힘이 다해 쓰러졌다.
    SIF BASUTESUCCESS:1 == 2
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 이미 독에 걸려있다.
    SIF BASUTESUCCESS:1 == 1
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 독에 걸렸다!
    SIF BASUTESUCCESS:2 == 2
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 이미 피를 흘리고 있다.
    SIF BASUTESUCCESS:2 == 1
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 피를 흘리고 있다!
    SIF BASUTESUCCESS:3 == 2
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 이미 겁을 먹고 있다.
    SIF BASUTESUCCESS:3 == 1
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 공포에 떨고 있다!
    SIF BASUTESUCCESS:4 == 2
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 이미 흥분해 있다.
    SIF BASUTESUCCESS:4 == 1
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% 제어할 수 없는 분노에 빠졌다!
    WAITANYKEY
    IF (CFLAG:TARGETV:버프적용중 & 1) != 0
        CALL EFFECT_COUNTER_ATTACK(TARGETV,USERV,ISENEMY^1)
    ENDIF
ENDIF

;적 전체 공격
@SKILL_ALL_ATTACK(USERV,SKILLV,ISENEMY=0)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC USERV
#DIM DYNAMIC SKILLV
#DIM DYNAMIC ISENEMY

IF !ISENEMY
    CFLAG:(FLAG:USERV):전회공격상대 = 7
    CFLAG:(FLAG:USERV):전회보조행동 = 0
    CALL BATTLE_TALK("SKILL",USERV,,SKILLV)
    SIF !RESULT
        PRINTFORML %조사처리(CALLNAME:(FLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
    PRINT ...
    WAITANYKEY
ELSEIF ISENEMY
    CFLAG:(TFLAG:USERV):전회공격상대 = 7
    CFLAG:(TFLAG:USERV):전회보조행동 = 0
    ;CALL ENEMY_BATTLETALK("SKILL",NO:USERV,,SKILLV)
    ;SIF !RESULT
        PRINTFORML ({USERV}) %조사처리(CALLNAME:(TFLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
    PRINT ...
    WAITANYKEY
ENDIF

IF !ISENEMY
    FOR LCOUNT,1,7
        SIF TFLAG:LCOUNT != 0 && !BATTLE_CANNOTTARGET(TFLAG:LCOUNT)
            CALL SKILL_SINGLE_ATTACK(LCOUNT,USERV,SKILLV,0,1)
    NEXT
ELSEIF ISENEMY
    FOR LCOUNT,1,3
        CALL SKILL_SINGLE_ATTACK(LCOUNT,USERV,SKILLV,0,1)
    NEXT
ENDIF

@SKILL_HEAL_SELF(USERV,SKILLV,ISENEMY=0)
#DIM DYNAMIC USERV
#DIM DYNAMIC SKILLV
#DIM DYNAMIC ISENEMY
#DIM DYNAMIC DAMAGEV
IF !ISENEMY
    CALL BATTLE_TALK("SKILL",USERV,,SKILLV)
    SIF !RESULT
        PRINTFORML %조사처리(CALLNAME:(FLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
ELSEIF ISENEMY
    PRINTFORML ({USERV}) %조사처리(CALLNAME:(TFLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
ENDIF
PRINT ...
WAITANYKEY
IF !ISENEMY
    USERV = FLAG:USERV
    CFLAG:(FLAG:USERV):전회보조행동 = 1
ELSEIF ISENEMY
    USERV = TFLAG:USERV
    CFLAG:(TFLAG:USERV):전회보조행동 = 1
ENDIF

IF SKILL_DATA:SKILLV:8 == 0 || RAND:100 > (100-SKILL_DATA:SKILLV:8)
    SELECTCASE SKILL_DATA:SKILLV:6
        CASE 0
            ;공격력 기반
            DAMAGEV = ((BASE:USERV:공격력) * (120-RAND:40)) / 100
            DAMAGEV = SKILL_CALC_ARMOR(USERV,SKILLV,DAMAGEV)
        CASE 1,2,3
            ;패기/예리/자비 대미지는 스탯 제곱근 * 베이스값 또한 난수에 의한 변동폭이 작다
            DAMAGEV = ((SKILL_DATA:SKILLV:4 * SQRT(BASE:USERV:(1+SKILL_DATA:SKILLV:6)) * (107-RAND:14))) / 100
            DAMAGEV = SKILL_CALC_ARMOR(USERV,SKILLV,DAMAGEV)
        CASE 4
            ;고정값
            DAMAGEV = SKILL_DATA:SKILLV:4
            DAMAGEV = SKILL_CALC_ARMOR(USERV,SKILLV,DAMAGEV)
    ENDSELECT
    BASE:USERV:희망 += DAMAGEV
    ;오버힐 스킬이 아닌 경우 체력을 최대치 이하로 한다
    IF (BASE:USERV:희망 > MAXBASE:USERV:희망) && (SKILL_DATA:SKILLV:16 == 0)
        BASE:USERV:희망 = MAXBASE:USERV:희망
    ENDIF
    IF DAMAGEV != 0
        PRINTFORML %조사처리(CALLNAME:USERV,"은")% {DAMAGEV} 만큼 회복했다!
        WAITANYKEY
    ENDIF
ELSE
    ;스킬 실패    
    PRINTL 실패했다!
    IF !ISENEMY
        CALL BATTLE_TALK("MISS",USERV)
    ELSEIF ISENEMY
        WAITANYKEY
    ENDIF
ENDIF

@SKILL_GUARD(USERV,SKILLV,ISENEMY=0)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC USERV
#DIM DYNAMIC SKILLV
#DIM DYNAMIC ISENEMY
IF !ISENEMY
    IF SKILLV == 1
        CALL BATTLE_TALK("GUARD",USERV)
    ELSE
        CALL BATTLE_TALK("SKILL",USERV,,SKILLV)
    ENDIF
    SIF !RESULT
        PRINTFORML %조사처리(CALLNAME:(FLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
ELSEIF ISENEMY
    PRINTFORML ({USERV}) %조사처리(CALLNAME:(TFLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
ENDIF
PRINT ...
WAITANYKEY
IF !ISENEMY
    USERV = FLAG:USERV
    CFLAG:(FLAG:USERV):전회보조행동 = 0
ELSEIF ISENEMY
    USERV = TFLAG:USERV
    CFLAG:(TFLAG:USERV):전회보조행동 = 0
ENDIF
;자신 부가효과 부여. 상태이상은 현재 미구현
FOR LCOUNT,12,16,2
    IF SKILL_DATA:SKILLV:LCOUNT != 0
        SELECTCASE SKILL_DATA:SKILLV:LCOUNT
            CASE 6;카운터부여
                IF (CFLAG:USERV:버프적용중 & 1) == 0
                    CFLAG:USERV:버프적용중 += 1
                    버프지속시간:USERV:0 = 2;발동 턴에 한번 사라지므로 2로 한다.
                    COUNTER_POWER:USERV:0 = SKILL_DATA:SKILLV:(LCOUNT+1)
                ENDIF
        ENDSELECT
    ENDIF
NEXT

;아직 예정이 없지만) 방어를 방해하는 요소가 있다면 여기에
CFLAG:USERV:방어 = 1

;도주 및 도주 스킬. 부가효과 미실장
@SKILL_ESCAPE(USERV,SKILLV,ISENEMY=0)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC USERV
#DIM DYNAMIC SKILLV
#DIM DYNAMIC ISENEMY

#DIM DYNAMIC TOTAL_ENEMY_LV
#DIM DYNAMIC MODV

;구상 출력
IF !ISENEMY
    IF SKILLV == 11
        CALL BATTLE_TALK("ESCAPE",USERV)
    ELSE
        CALL BATTLE_TALK("SKILL",USERV,,SKILLV)
    ENDIF
    SIF !RESULT
        PRINTFORML %조사처리(CALLNAME:(FLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
    PRINTL ...
    WAITANYKEY
ELSEIF ISENEMY
    PRINTFORML ({USERV}) %조사처리(CALLNAME:(TFLAG:USERV),"은")% %SKILL_TEXT:SKILLV%
    PRINTL ...
    WAITANYKEY0
ENDIF

;레벨에 의한 확률 보정치 계산
IF !ISENEMY
    ;도주하는 것이 아군일때
    FOR LCOUNT,1,7
        SIF TFLAG:LCOUNT
            TOTAL_ENEMY_LV += BASE:(TFLAG:LCOUNT):레벨
    NEXT
    MODV = BASE:(FLAG:USERV):레벨 - (TOTAL_ENEMY_LV / TFLAG:적캐릭터초기화)
ELSE
    ;도주하는 것이 적일때
    MODV = BASE:(TFLAG:USERV):레벨 - ((BASE:(FLAG:1):레벨 + BASE:(FLAG:2):레벨) / 2)
ENDIF
TIMES MODV, 2

IF MODV >= 100 - (SKILL_DATA:SKILLV:8)
    MODV = 100 - (SKILL_DATA:SKILLV:8)
ELSEIF MODV <= -(SKILL_DATA:SKILLV:8)
    MODV = -(SKILL_DATA:SKILLV:8)
ENDIF
SELECTCASE RAND:100
    CASE 0 TO MODV+(SKILL_DATA:SKILLV:8)
        PRINTL 도망쳤다!
        PRINTL ...
        WAITANYKEY
        ;도주처리
        IF !ISENEMY
            CALL BATTLE_TALK("ESCAPE_SUCC",USERV)
            CALL BATTLE_TALK("ESCAPE_SUCC",USERV^3,1)
            TFLAG:전투결과 == 3
        ELSE
            ;도주하는 것이 적:
            ENEMY_GONE:(TFLAG:USERV):0 = 1
        ENDIF
    CASEELSE
        PRINTL 실패했다!
        WAITANYKEY
        SIF !ISENEMY
            CALL BATTLE_TALK("ESCAPE_FAIL",USERV)
        ;도주실패
ENDSELECT

@SKILL_CALC_ARMOR(TARGETV,SKILLV,DAMAGEV)
#FUNCTION
#DIM DYNAMIC TARGETV
#DIM DYNAMIC SKILLV
#DIM DYNAMIC DAMAGEV
SELECTCASE SKILL_DATA:SKILLV:7
    CASE 0
        ;방어 무시
        RETURNF DAMAGEV
    CASE 1
        ;방어력 계산 시 예리 *2의 제곱근 + 방어력으로 나눈다
        DAMAGEV = DAMAGEV / (SQRT((BASE:TARGETV:예리 * 2) + BASE:TARGETV:방어력) + 1)
        SIF CFLAG:TARGETV:방어
            TIMES DAMAGEV, 0.50
        RETURNF DAMAGEV
    CASE 2
        ;방어력 반감
        DAMAGEV = DAMAGEV / (SQRT((BASE:TARGETV:예리 * 2) + BASE:TARGETV:방어력)/2 + 1)
        SIF CFLAG:TARGETV:방어
            TIMES DAMAGEV, 0.50
        RETURNF DAMAGEV
    CASE 3
        ;전체HP비례피해
        RETURNF MAXBASE:TARGETV:희망 * (DAMAGE/100)
ENDSELECT

@EFFECT_COUNTER_ATTACK(USERV,TARGETV,ISENEMY=0)
#DIM DYNAMIC USERV
#DIM DYNAMIC TARGETV
#DIM DYNAMIC ISENEMY
#DIM DYNAMIC DAMAGEV

SIF !ISENEMY
    CALL BATTLE_TALK("COUNTER",USERV)
PRINTFORML %조사처리(CALLNAME:USERV,"은")% 반격을 구사했다.
PRINT ...
WAITANYKEY
IF RAND:100 <= (100-SKILL_DATA:0:8)+(BASE:TARGETV:회피율)-(SKILL_DATA:0:9)
    PRINTL 빗나갔다!
    WAITANYKEY
ELSE
    DAMAGEV = ((BASE:USERV:공격력) * (120-RAND:40)) / 100
    DAMAGEV = SKILL_CALC_ARMOR(TARGETV,0,DAMAGEV)
    ;격앙처리
    SIF (CFLAG:USERV:상태이상 & 16) != 0
        TIMES DAMAGEV, 1.20
    SIF (CFLAG:USERV:상태이상 & 16) != 0
        TIMES DAMAGEV, 1.20
    ;HP 감소
    BASE:TARGETV:희망 -= DAMAGEV
    
    DAMAGEV = (DAMAGEV * COUNTER_POWER:USERV:0) / 10

    SIF DAMAGEV != 0
        PRINTFORML %조사처리(CALLNAME:TARGETV,"은")% {DAMAGEV} 피해를 받았다!
    WAITANYKEY
ENDIF