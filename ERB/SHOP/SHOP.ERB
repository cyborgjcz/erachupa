;-----------------------------------------------------------
;SHOW_SHOP
;BEGIN SHOP, LOADDATA 등 통칭 메인 화면 표시 함수
;-----------------------------------------------------------
@SHOW_SHOP
CALL REAL_DAILY
SELECTCASE BGM_BEFORE_BATTLE
    CASE "s1.mp3"
        IF FLAG:탐색대상던전 != 0
            STOPBGM
            PLAYBGM "s2.mp3"
            BGM_BEFORE_BATTLE '= "s2.mp3"
        ENDIF
    CASE "s2.mp3"
        ;.
    CASEELSE
            STOPBGM
        IF FLAG:탐색대상던전 != 0
            PLAYBGM "s2.mp3"
            BGM_BEFORE_BATTLE '= "s2.mp3"
        ELSE
            PLAYBGM "s1.mp3"
            BGM_BEFORE_BATTLE '= "s1.mp3"
        ENDIF
ENDSELECT
;200 ~ 299 일상 관련
SETCOLOR 0xfee12b
PRINT 【
RESETCOLOR
PRINT 일상 관련
SETCOLOR 0xfee12b
PRINTL 】
RESETCOLOR
PRINTFORMLC [200] 극장을 살핀다
PRINTFORMLC [201] 거리를 살핀다
PRINTFORMLC [202] 작전 회의
PRINTL
PRINTL
;300 ~ 399 모험 관련
;스토리 던전 탐색 시작 버튼
SETCOLOR 0xc7ea46
PRINT 【
RESETCOLOR
PRINT 모험 관련
SETCOLOR 0xc7ea46
PRINTL 】
RESETCOLOR
IF FLAG:탐색대상던전 == 0
    SETCOLORBYNAME DimGray
    PRINTLC [???] ???
    RESETCOLOR
ELSEIF FLAG:탐색대상던전 != 0
    PRINTFORMLC [300] %조사처리(DUNGEONNAME:(FLAG:탐색대상던전),"으로")%
ENDIF
PRINTL
PRINTL
SETCOLOR 0x32cd32
PRINT 【
RESETCOLOR
PRINT 정비 관련
SETCOLOR 0x32cd32
PRINTL 】
RESETCOLOR
;400 ~ 499 정비 관련
RESETCOLOR
PRINTLC [400] 미사키를 찾아간다
PRINTLC [401] 소라를 찾아간다
PRINTL
IF FLAG:탐색대상던전 == 0
    SETCOLORBYNAME DimGray
    PRINTLC [???] ???
    PRINTLC [???] ???
    PRINTLC [???] ???
    RESETCOLOR
ELSEIF FLAG:탐색대상던전 != 0
    PRINTLC [500] 능력 확인
    PRINTLC [501] 장비품
    PRINTLC [502] 스킬
ENDIF
PRINTL
PRINTL
SETCOLOR 0x008080
PRINT 【
RESETCOLOR
PRINT 시스템 관련
SETCOLOR 0x008080
PRINTL 】
RESETCOLOR
PRINTLC [600] 세이브
PRINTLC [700] 로드
PRINTLC [800] 컨피그
PRINTL
PRINTLC [900] 타이틀
;-----------------------------------------------------------
;USERSHOP
;SHOW_SHOP 출력 후 0~99 밖의 입력을 받으면 처리하는 함수
; SHOP_EVENT 종류 함수 실행 후 RESULT 반환값이 참(1)이라면
; 뒤이어 후속 이벤트를 실행한다. 상점주인과 대화 후 상점 느낌
;-----------------------------------------------------------
@USERSHOP
#DIM DYNAMIC KEY
KEY = RESULT
RESULT = 1
SELECTCASE KEY
    CASE 0 TO 99
        RETURN 0
    CASE 200
        SIF !FLAG:스토리1진행도
            CALL SHOP_EVENT_CHAPTER0(200)
        IF RESULT == 1
            ;통상 극장 살펴보기 이벤트
        ENDIF
    CASE 201
        SIF !FLAG:스토리1진행도
            CALL SHOP_EVENT_CHAPTER0(201)
        IF RESULT == 1
            ;통상 거리 살펴보기 이벤트
        ENDIF
    CASE 202
        SIF !FLAG:스토리1진행도
            CALL SHOP_EVENT_CHAPTER0(202)
    CASE 300
        SIF !FLAG:스토리1진행도
            CALL SHOP_EVENT_CHAPTER0(300)
        IF RESULT == 1
            IF FLAG:탐색대상던전 > 0
                CALL DUNGEON_START(FLAG:탐색대상던전)
            ENDIF
        ENDIF
    CASE 400
        IF !FLAG:스토리1진행도
            CALL SHOP_EVENT_CHAPTER0(400)
        ENDIF
        IF RESULT == 1
            ;통상 미사키 이벤트
        ENDIF
    CASE 401
        SIF !FLAG:스토리1진행도
            CALL SHOP_EVENT_CHAPTER0(401)
        IF RESULT == 1
            ;통상 소라 이벤트
        ENDIF
    CASE 500
        SIF FLAG:탐색대상던전 != 0
            CALL SHOW_CHARA_INFO(FLAG:1)
    CASE 501
        PRINTL 누구의 장비를 설정합니까?
        PRINTBUTTON @"[1] %CALLNAME:(FLAG:1)%",1
        PRINTBUTTON @"[2] %CALLNAME:(FLAG:2)%",2
        INPUT
        SELECTCASE RESULT
            CASE 1 TO 2
                CALL SHOW_SET_EQUIP(FLAG:RESULT)
        ENDSELECT
    CASE 600
        SAVEGAME
    CASE 700
        LOADGAME
    CASE 800
        PRINTL 미실장
    CASE 900
        PRINTL 정말로 타이틀로 돌아갑니까?
        PRINTBUTTON "[0] 예",0
        PRINTBUTTON "[1] 아니오",1
        INPUT
        IF !RESULT
            RESETDATA
            BEGIN TITLE
        ENDIF
ENDSELECT