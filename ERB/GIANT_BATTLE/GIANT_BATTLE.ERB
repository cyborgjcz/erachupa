;좌표를 인수로 받아 거대로봇전투 적 또는 폭격 경보의 번호를 전달.
;해당 좌표가 비었다면 -1을 전달
@GET_GIANT_ENEMY_NUM(XV,YV)
#FUNCTION
#DIM DYNAMIC XV
#DIM DYNAMIC YV
#DIM DYNAMIC LOOPCOUNT
#DIM DYNAMIC VALUEV
DO
    IF GIANT_ENEMY:LOOPCOUNT:0 == XV && GIANT_ENEMY:LOOPCOUNT:1 == YV && GIANT_ENEMY:LOOPCOUNT:2 > 0
        VALUEV = LOOPCOUNT
        BREAK
    ELSEIF GIANT_ALERT:LOOPCOUNT:0 == XV && GIANT_ALERT:LOOPCOUNT:1 == YV && GIANT_ALERT:LOOPCOUNT:2 > 0
        VALUEV = LOOPCOUNT + 361
        BREAK
    ENDIF
    LOOPCOUNT += 1    
    IF LOOPCOUNT >= 361
        VALUEV = -1
        BREAK
    ENDIF
LOOP 1
RETURNF VALUEV

@GET_GIANT_ENEMY_ID(NUMV)
#FUNCTION
#DIM DYNAMIC NUMV
IF NUMV < 361
    RETURNF GIANT_ENEMY:NUMV:2
ELSE
    RETURNF GIANT_ALERT:(NUMV - 361):2
ENDIF

@CHECK_RIGHT_THERE_GIANT_ENEMY
#DIM DYNAMIC LCOUNT
CALL D3D_GET_FRONT_COORD(D3D_PLAYER_XCOORD,D3D_PLAYER_YCOORD)
SELECTCASE GET_GIANT_ENEMY_NUM(RESULT:0,RESULT:1)
    CASE -1
        RETURN 0
    CASE 0 TO 360
        RETURN 1
    CASE 361 TO 721
        RETURN 0
ENDSELECT

;전도 대미지 계산
@GET_DOWN_DAMAGE_GIGANT(DAMAGEV)
#DIM DYNAMIC DAMAGEV
#DIM DYNAMIC STABILITYV
#DIM DYNAMIC NUMV
SIF GIGANT_TUMBLE != 0
    RETURN 0
;피격 불안정 대미지 보정. 이미 대미지를 받고 있을수록 받는 양이 커진다
STABILITYV = GET_GIGANT_TOTAL_STAT(GIGANT_안정성)
NUMV = (STABILITYV / 100) * 25
SELECTCASE STABILITYV - GIGANT_DOWN_DAMAGE
    CASE NUMV * 3 + 1 TO STABILITYV
        TIMES DAMAGEV, 0.35
    CASE NUMV * 2 + 1 TO NUMV * 3
        TIMES DAMAGEV, 0.70
    CASE NUMV + 1 TO NUMV * 2
        TIMES DAMAGEV, 1.05
    CASE 1 TO NUMV
        TIMES DAMAGEV 1.15
ENDSELECT
;자세 불안정 보정
SELECTCASE GIGANT_UNSTABILITY
    CASE 0
    CASE 1
        TIMES DAMAGEV 1.25
    CASE 2
        TIMES DAMAGEV 1.50
    CASE 3
        TIMES DAMAGEV 1.75
    CASEELSE
        TIMES DAMAGEV 2.00
ENDSELECT

GIGANT_DOWN_DAMAGE += DAMAGEV
SIF GIGANT_DOWN_DAMAGE >= STABILITYV
    CALL GET_TUMBLE_GIGANT
RETURN 1

;이벤트 구상 출력과 연출 등을 할 예정이므로 자리만 만든다
@GET_TUMBLE_GIGANT
CALL SHAKE_SCREEN
GIGANT_TUMBLE = 1
RETURN 1

;그저 기능을 명확하게 해두기 위한 함수
@DOWN_DAMAGE_RECOVER
SIF !GIGANT_TUMBLE
    TIMES GIGANT_DOWN_DAMAGE, 0.9
RETURN 1