;FileName_TITLE.ERB -------------------------------- 
;eraTWKR, YM로부터 차용 및 개변

@SYSTEM_TITLE
#DIMS DYNAMIC VERSIONSTRING
#DIM DYNAMIC FOO
#DIM DYNAMIC LINENUM

;타이틀 드로잉
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIMS DYNAMIC BOXA,2 = "■","□"
{
	#DIM DYNAMIC DRAWINGA,211 = 27, 2, 38, 0, 6, 0,
	25, 6, 3, 3, 30, 0, 6, 0,
	24, 8, 2, 3, 30, 0, 6, 0,
	3, 2, 6, 2, 4, 1, 5, 10, 1, 3, 1, 1, 14, 2, 7, 2, 3, 0, 6, 0,
	1, 6, 3, 3, 2, 5, 3, 3, 3, 4, 1, 6, 3, 2, 1, 3, 3, 5, 3, 6, 1, 0, 6,
	8, 1, 4, 1, 7, 2, 3, 8, 7, 2, 2, 1, 3, 2, 7, 1, 8, 6,
	3, 1, 4, 1, 3, 2, 3, 1, 3, 2, 3, 4, 3, 1, 3, 1, 3, 2, 2, 1, 3, 2, 2, 2, 3, 1, 3, 1, 4, 6,
	3, 6, 3, 2, 2, 2, 3, 2, 4, 2, 4, 1, 3, 1, 3, 2, 2, 1, 3, 1, 3, 2, 3, 1, 3, 2, 3, 6,
	5, 4, 3, 2, 7, 2, 9, 2, 3, 1, 3, 2, 6, 1, 8, 1, 8, 6, 0,
	1, 5, 3, 3, 2, 7, 3, 7, 3, 3, 1, 3, 2, 5, 2, 7, 3, 7, 6, 0,
	2, 4, 3, 3, 3, 6, 4, 5, 4, 3, 1, 3, 3, 4, 2, 6, 5, 6, 6, 0,
	50, 3, 14, 0, 6, 0,
	50, 3, 14, 0, 6,
}
#DIM DYNAMIC CURSORA,3 ;0 = DRAWINGA 탐색 1 = DRAWINGA 원소 카운팅 2 = BOXA 탐색

LOADGLOBAL
RESULT = GLOBAL
SAVEGLOBAL
LOADGLOBAL

CLEARLINE 1

;버전 표기 연산 1001이라면 1.001, 1100라면 1.10이라고 표시되도록.
VERSIONSTRING = {GAMEBASE_VERSION / 1000}.%TOSTR(GAMEBASE_VERSION % 1000 / 10,"00")%
SIF GAMEBASE_VERSION % 10 != 0
    VERSIONSTRING = %TOSTR(GAMEBASE_VERSION / 1000)%{GAMEBASE_VERSION % 10}

REDRAW 0
REPEAT 11
	PRINTL
REND
RESULT  = 1
LINENUM = LINECOUNT

STOPBGM
SIF EXISTSOUND("t2.mp3")
	PLAYBGM "t2.mp3"


DO
	$START
	CLEARLINE LINECOUNT - LINENUM	

	FOR LCOUNT,0,13
		FOR LCOUNT2,0,67+6
			IF CURSORA:1 < DRAWINGA:(CURSORA:0)
				IF CURSORA:2 == 0
					SELECTCASE RAND:6
						CASE 0
							SETCOLORBYNAME Pink
						CASE 1
							SETCOLORBYNAME LightBlue
						CASE 2
							SETCOLORBYNAME LightYellow
						CASE 3 TO 5
							SETCOLORBYNAME White
					ENDSELECT
				ELSE
					SETCOLORBYNAME White
				ENDIF
				PRINTS BOXA:(CURSORA:2)
				RESETCOLOR
				CURSORA:1 ++
			ELSE
				CURSORA:0 ++
				CURSORA:1 = 0
				CURSORA:2 = (CURSORA:2)^1
				LCOUNT2 -= 1
			ENDIF
		NEXT
		PRINTL
	NEXT
	VARSET CURSORA

	ALIGNMENT LEFT
	CUSTOMDRAWLINE ━
	{
	PRINTFORML %GAMEBASE_TITLE%
	\nv%VERSIONSTRING%
	\n작자: %GAMEBASE_AUTHOR%
	\n(%GAMEBASE_YEAR%)
	\n\n%GAMEBASE_INFO%
	}
	CUSTOMDRAWLINE ━
	PRINTFORML [0] 시작하기
	PRINTFORML [1] 불러오기
	ALIGNMENT LEFT
	TONEINPUT 188 * 4, 2, 0, ""
	CUSTOMDRAWLINE ━
	SELECTCASE RESULT
		CASE 0
			RESETDATA
			ADDCHARA 0
			BEGIN FIRST
		CASE 1
			LOADGAME
	ENDSELECT
	GOTO START
LOOP RESULT
REDRAW 1