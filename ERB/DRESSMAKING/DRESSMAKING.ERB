@DRESSMAKING_SCENE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ITEM_LISTA,1000
#DIM DYNAMIC ITEM_NUMV

#DIM DYNAMIC EASE_A,3 = 5,22,5
#DIM DYNAMIC SPACE_A,24

#DIMS DYNAMIC DISPLAYSA,24
#DIM DYNAMIC DISPLAYCA,24
#DIM DYNAMIC CURSORV
#DIM DYNAMIC KEYV

#DIM DYNAMIC ANIMKEY
#DIMS DYNAMIC BUFFERSA,2
#DIM DYNAMIC BUFFERCA,2
#DIM DYNAMIC ALL_COLOR

#DIM DYNAMIC ASK_DECIDE

#DIM DYNAMIC LOOPSWITCH
#DIM DYNAMIC ERASEMORE
#DIM DYNAMIC WILLRESTART

CBGCLEAR
GCREATEFROMFILE 0, @"white_misaki.png"
CBGSETG 0, 620, -20, 1
;SPACE_A 2차 함수로 작성
FOR LCOUNT,0,24
    SPACE_A:LCOUNT = 10 - (POWER(LCOUNT - 12,2)/ 14)
NEXT

VARSET ITEM_LISTA
;DRESSMAKING_LIST와 매치하는 장비 검색
FOR LCOUNT,1001,2001
    SIF DRESSMAKING_DATA(LCOUNT,DRESSMAKING_MAT_LEN) == -1
        CONTINUE
    ITEM_LISTA:ITEM_NUMV = LCOUNT
    ITEM_NUMV ++
NEXT

WILLRESTART = 0
LOOPSWITCH = 1
DO
    ERASEMORE = 0
    PRINTL
    PRINTL
    PRINTL

    VARSET DISPLAYSA
    FOR LCOUNT,0,8
        SELECTCASE LCOUNT
            CASE 0 TO 3
                SIF CURSORV < (4 - LCOUNT)
                    CONTINUE
        ENDSELECT
        SIF (LCOUNT-4+CURSORV) >= ITEM_NUMV
            CONTINUE
        KEYV = ITEM_LISTA:(LCOUNT-4+CURSORV)
        CALL DRESSMAKING_SETCOLOR(KEYV,ALL_COLOR)
        GETCOLOR
        DISPLAYCA:(3 * LCOUNT) = RESULT,RESULT,RESULT
        DISPLAYSA:(3 * LCOUNT) = %ITEMNAME:KEYV%  소지수 : {ITEM:KEYV}
        DISPLAYSA:((3 * LCOUNT)+1) '= @"%ITEMNAME:DRESSMAKING_DATA(KEYV,1)%"
        DISPLAYSA:((3 * LCOUNT)+1) += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,1),"D2"))% / "
        DISPLAYSA:((3 * LCOUNT)+1) += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT1_NUM),"D2"))%"
        IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 2
            DISPLAYSA:((3 * LCOUNT)+1) += @"%ITEMNAME:DRESSMAKING_DATA(KEYV,2)%"
            DISPLAYSA:((3 * LCOUNT)+1) += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,2),"D2"))% / "
            DISPLAYSA:((3 * LCOUNT)+1) += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT2_NUM),"D2"))%"
        ENDIF
        IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 3
            DISPLAYSA:((3 * LCOUNT)+2) '= @"%ITEMNAME:DRESSMAKING_DATA(KEYV,3)%"
            DISPLAYSA:((3 * LCOUNT)+2) += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,3),"D2"))% / "
            DISPLAYSA:((3 * LCOUNT)+2) += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT3_NUM),"D2"))%"
        ENDIF
        IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 4
            DISPLAYSA:((3 * LCOUNT)+2) += @"%ITEMNAME:DRESSMAKING_DATA(KEYV,3)%"
            DISPLAYSA:((3 * LCOUNT)+2) += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,4),"D2"))% / "
            DISPLAYSA:((3 * LCOUNT)+2) += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT4_NUM),"D2"))%"
        ENDIF
        RESETCOLOR
    NEXT

    VARSET BUFFERSA
    ;버퍼 문자열 세팅
    SELECTCASE ANIMKEY
        CASE -3 TO -1
            IF (CURSORV-4) > 0
                KEYV = ITEM_LISTA:(CURSORV-4)
                CALL DRESSMAKING_SETCOLOR(KEYV,ALL_COLOR)
                GETCOLOR
                BUFFERCA:0 = RESULT,RESULT
                BUFFERSA:0 '= @"%ITEMNAME:DRESSMAKING_DATA(KEYV,1)%"
                BUFFERSA:0 += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,1),"D2"))% / "
                BUFFERSA:0 += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT1_NUM),"D2"))%"
                IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 2
                    BUFFERSA:0 += @"%ITEMNAME:DRESSMAKING_DATA(KEYV,2)%"
                    BUFFERSA:0 += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,2),"D2"))% / "
                    BUFFERSA:0 += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT2_NUM),"D2"))%"
                ENDIF
                IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 3
                    BUFFERSA:1 '= @"%ITEMNAME:DRESSMAKING_DATA(KEYV,3)%"
                    BUFFERSA:1 += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,3),"D2"))% / "
                    BUFFERSA:1 += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT3_NUM),"D2"))%"
                ENDIF
                IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 4
                    BUFFERSA:1 += @"%ITEMNAME:DRESSMAKING_DATA(KEYV,4)%"
                    BUFFERSA:1 += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,4),"D2"))% / "
                    BUFFERSA:1 += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT4_NUM),"D2"))%"
                ENDIF
                RESETCOLOR
            ENDIF
        CASE 1 TO 3
            IF (CURSORV+4) < ITEM_NUMV
                KEYV = ITEM_LISTA:(CURSORV+4)
                CALL DRESSMAKING_SETCOLOR(KEYV,ALL_COLOR)
                GETCOLOR
                BUFFERCA:0 = RESULT,RESULT
                BUFFERSA:0 = %ITEMNAME:KEYV%
                BUFFERSA:1 '= @"%ITEMNAME:DRESSMAKING_DATA(KEYV,1)%"
                BUFFERSA:1 += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,1),"D2"))% / "
                BUFFERSA:1 += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT1_NUM),"D2"))%"
                IF DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN) >= 2
                    BUFFERSA:1 += @"%ITEMNAME:DRESSMAKING_DATA(KEYV,2)%"
                    BUFFERSA:1 += @"%TOFULL(TOSTR(ITEM:DRESSMAKING_DATA(KEYV,2),"D2"))% / "
                    BUFFERSA:1 += @"%TOFULL(TOSTR(DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT2_NUM),"D2"))%"
                ENDIF
                RESETCOLOR
            ENDIF
    ENDSELECT

    KEYV = ITEM_LISTA:CURSORV
    ;공백 작성
    FOR LCOUNT,0,24
        IF LCOUNT != 12
            PRINT_SPACE 350 - (25 * (1 + SPACE_A:LCOUNT))
        ELSE
            SETCOLORBYNAME Yellow
            PRINT ♪
            RESETCOLOR
        ENDIF
        ;ANIMKEY 값에 따라 한 둘 늘여서 문자 표시, 그리고 비는 공간에 버퍼 문자열 출력
        IF LCOUNT+ANIMKEY >= 0 && LCOUNT+ANIMKEY < 24
            SETCOLOR DISPLAYCA:(LCOUNT+ANIMKEY)
            PRINTSL DISPLAYSA:(LCOUNT+ANIMKEY)
            RESETCOLOR
        ELSE
            SELECTCASE LCOUNT+ANIMKEY
                CASE 24,-2
                    SETCOLOR BUFFERCA:0
                    PRINTSL BUFFERSA:0
                    RESETCOLOR
                CASE 25,-1
                    SETCOLOR BUFFERCA:1
                    PRINTSL BUFFERSA:1
                    RESETCOLOR
            ENDSELECT
        ENDIF
    NEXT
    
    SIF ASK_DECIDE
        SETCOLORBYNAME DimGray
    PRINTBUTTON "[w] ↑ ","w"
    PRINTBUTTON "[s] ↓ ","s"
    PRINTBUTTON "[a] ← ","a"
    PRINTBUTTON "[d] → ","d"
    RESETCOLOR
    PRINTBUTTON "[u] 결정","u"
    PRINTBUTTON "[x] 돌아간다","x"
    PRINT_SPACE 1500
    IF ASK_DECIDE
        PRINTFORML 의상 %조사처리(ITEMNAME:(ITEM_LISTA:CURSORV),"으로")% 결정할까요?
    ELSE
        PRINTL 제작할 의상을 선택해주세요.
    ENDIF

    ;글자 넘기는 매우 짧은 애니메이션 처리. 자동으로 넘김
    IF ALL_COLOR > 0
        TWAIT 40,1
        SIF ALL_COLOR > 0
            ALL_COLOR --
    ELSEIF ANIMKEY != 0
        TWAIT EASE_A:(ABS(ANIMKEY)-1),1
        IF ANIMKEY > 0
            ANIMKEY --
        ELSEIF ANIMKEY < 0
            ANIMKEY ++
        ENDIF
    ELSE
        ONEINPUTS
        IF ASK_DECIDE
            SELECTCASE RESULTS
                CASE "u"
                    CALL DRESSMAKING_MAKE(ITEM_LISTA:CURSORV)
                    ASK_DECIDE = 0
                    LOOPSWITCH = 0
                    WILLRESTART = 1
                    ERASEMORE = 1
                CASE "x"
                    ASK_DECIDE = 0
                    ERASEMORE = 1
                CASE ""
                CASEELSE
                    ERASEMORE = 1
            ENDSELECT
        ELSE
            SELECTCASE RESULTS
                CASE "w"
                    IF CURSORV > 0
                        CURSORV --
                        ANIMKEY += 3
                    ELSE
                        ERASEMORE = 1
                    ENDIF
                CASE "s"
                    IF CURSORV < (ITEM_NUMV-1)
                        CURSORV ++
                        ANIMKEY -= 3
                    ELSE
                        ERASEMORE = 1
                    ENDIF
                CASE "a"
                    IF CURSORV-4 > -1
                        CURSORV -= 4
                        ALL_COLOR = 3
                        ERASEMORE = 1
                    ELSE
                        ERASEMORE = 1
                    ENDIF
                CASE "d"
                    IF CURSORV+4 < (ITEM_NUMV)
                        CURSORV += 4
                        ALL_COLOR = 3
                        ERASEMORE = 1
                    ELSE
                        ERASEMORE = 1
                    ENDIF
                CASE "u"
                    IF DRESSMAKING_ABLE(ITEM_LISTA:CURSORV)
                        ASK_DECIDE = 1
                        ERASEMORE = 1
                    ELSE
                        ERASEMORE = 1
                    ENDIF
                CASE "x"
                    LOOPSWITCH = 0
                CASE ""
                    ERASEMORE = -1
                CASEELSE
                    ERASEMORE = 1
            ENDSELECT
        ENDIF
    ENDIF
    CLEARLINE 28+ERASEMORE
    REUSELASTLINE
LOOP LOOPSWITCH
CBGCLEAR
SIF WILLRESTART
    RESTART

@DRESSMAKING_SETCOLOR(KEYV,ALL_COLOR)
#DIM DYNAMIC KEYV
#DIM DYNAMIC ALL_COLOR
IF ALL_COLOR > 0
    SELECTCASE ALL_COLOR
        CASE 1
            SETCOLORBYNAME DimGray
        CASE 2
            SETCOLORBYNAME Gray
        CASE 3
            GETDEFBGCOLOR
            SETCOLOR RESULT
    ENDSELECT
ELSEIF DRESSMAKING_ABLE(KEYV) == 0
    SETCOLORBYNAME DimGray
    DEBUGPRINTFORML Set DimGray 220Line
ENDIF

;제작가능 판정
@DRESSMAKING_ABLE(KEYV)
#FUNCTION
#DIM DYNAMIC KEYV
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC OKCHECK

FOR LCOUNT,1,DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN)+1
    IF ITEM:DRESSMAKING_DATA(KEYV,LCOUNT) >= DRESSMAKING_DATA(KEYV,LCOUNT+4)
        OKCHECK ++
    ELSE
        CONTINUE
    ENDIF
NEXT
IF OKCHECK < DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN)
    RETURNF 0
ELSE
    RETURNF 1
ENDIF

@DRESSMAKING_MAKE(KEYV)
#DIM DYNAMIC KEYV
#DIM DYNAMIC LCOUNT
FOR LCOUNT,1,DRESSMAKING_DATA(KEYV,DRESSMAKING_MAT_LEN)+1
    ITEM:DRESSMAKING_DATA(KEYV,LCOUNT) -= DRESSMAKING_DATA(KEYV,LCOUNT+4)
NEXT
ITEM:KEYV += 1